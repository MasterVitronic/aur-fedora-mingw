# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-gettext.git/

pkgname=$CARCH-mingw-w64-gettext
pkgver=0.19.7
pkgrel=3
pkgdesc='GNU internationalization library (mingw-w64)'
arch=(any)
url='http://www.gnu.org/software/gettext/'
license=(GPL)
groups=(afm-$CARCH)
depends=(
    mingw-w64-crt
    $CARCH-mingw-w64-win-iconv
    $CARCH-mingw-w64-termcap
)
makedepends=(afm-mingw-w64-makedepends)
source=("http://ftp.gnu.org/pub/gnu/gettext/gettext-$pkgver.tar.gz")
md5sums=('87c4ab267c4dce8a75db5d057bb3c92f')

build() {
  local tardir="$srcdir/gettext-$pkgver"
  local builddir="$tardir-build"

  mkdir -p "$builddir"
  cd "$builddir"
  source $CHOST-configure "$tardir" \
    CPPFLAGS="$CPPFLAGS -I../../gettext-runtime/intl" \
    --enable-static \
    --enable-shared \
    --disable-silent-rules \
    --disable-java \
    --disable-native-java \
    --disable-csharp \
    --enable-threads=win32 \
    --without-emacs \
    --disable-openmp
  make
}

package() {
  local prefix="$pkgdir/usr/$CHOST"

  cd "$srcdir/gettext-$pkgver-build"
  make DESTDIR="$pkgdir" install

  find "$prefix" -name '*.exe' -exec $CHOST-strip {} +
  find "$prefix" -name '*.dll' -exec $CHOST-strip --strip-unneeded {} +
  find "$prefix" -name '*.a'   -exec $CHOST-strip -g {} +

  # Remove components selectively
  rm -fr "$prefix/lib/gettext"
  rm -fr "$prefix/lib/gettext"
  rm -f  "$prefix/share/locale/locale.alias"
}

# vim:set ts=2 sw=2 et:
