# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-gettext.git/

_realname=gettext
pkgname="fedora-mingw-w64-${_realname}"
pkgver=0.19.4
pkgrel=6
pkgdesc='GNU internationalization library (mingw-w64)'
arch=(any)
url='http://www.gnu.org/software/gettext/'
license=(GPL)
groups=(fedora-mingw)
depends=(mingw-w64-crt fedora-mingw-w64-win-iconv fedora-mingw-w64-termcap)
makedepends=(afm-mingw-w64-makedepends)
provides=("mingw-w64-${_realname}=$pkgver")
conflicts=("mingw-w64-${_realname}")
options=(!strip staticlibs !buildflags)
source=("http://ftp.gnu.org/pub/gnu/${_realname}/${_realname}-$pkgver.tar.gz")
md5sums=('d3511af1e604a3478900d2c2b4a4a48e')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

build() {
  local tardir="$srcdir/${_realname}-$pkgver"
  local builddir
  local arch

  for arch in ${_architectures[@]}; do
    builddir="$tardir-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    MINGW_CPPFLAGS=-I../../gettext-runtime/intl \
    source $arch-configure "$tardir" \
      --enable-static \
      --enable-shared \
      --disable-silent-rules \
      --disable-java \
      --disable-native-java \
      --disable-csharp \
      --enable-threads=win32 \
      --without-emacs \
      --disable-openmp
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/${_realname}-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    find "$prefix" -regex '.*\.\(exe\|bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip --strip-unneeded {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +

    # Remove components selectively
    rm -fr "$prefix/share/doc"
    rm -fr "$prefix/share/gettext"
    rm -fr "$prefix/share/info"
    rm -fr "$prefix/share/man"
    rm -fr "$prefix/lib/gettext"
    rm -fr "$prefix/lib/gettext"
    rm -f  "$prefix/share/locale/locale.alias"
  done
}

# vim:set ts=2 sw=2 et:
