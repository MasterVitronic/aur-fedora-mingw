# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-gettext.git/

pkgname=fedora-mingw-w64-gettext
pkgver=0.18.1.1
pkgrel=1
pkgdesc='GNU internationalization library (mingw-w64)'
arch=(any)
url='http://www.gnu.org/software/gettext/'
license=(GPL)
groups=(fedora-mingw)
depends=(mingw-w64-crt fedora-mingw-w64-win-iconv fedora-mingw-w64-termcap)
makedepends=('mingw-w64-gcc>=4.7.0' autoconf automake libtool)
provides=("mingw-w64-gettext=$pkgver")
conflicts=(mingw-w64-gettext)
options=(!strip !buildflags !libtool)
source=("http://ftp.gnu.org/pub/gnu/gettext/gettext-$pkgver.tar.gz"
        "http://pkgs.fedoraproject.org/cgit/mingw-gettext.git/plain/gettext-0.18.1.1-tml.patch?h=f18")
md5sums=('3dd55b952826d2b32f51308f2f91aa89'
         'daa03e4e4549fbab89b84889e748b355')

_architectures=(i686-w64-mingw32
                x86_64-w64-mingw32)

build() {
  local tardir="$srcdir/${pkgname#fedora-mingw-w64-}-$pkgver"
  local builddir
  local arch

  # Apply patches from fedora repository
  cd "$tardir"
  patch -Np0 < '../gettext-0.18.1.1-tml.patch?h=f18'

  # The libtool bundled with this package is too old for win64 support
  autoreconf -if

  export CFLAGS='-O2 -pipe'
  export CXXFLAGS="$CFLAGS"
  unset  LDFLAGS
  export gl_cv_func_memchr_works="yes"
  export ac_cv_func_strnlen_working="yes"

  for arch in ${_architectures[@]}; do
    builddir="$srcdir/$pkgname-$pkgver-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    # No idea why but CXX must be explicitely set because the
    # gettext-tools subproject fails to autodetect it. Others
    # MingW scripts, such as Fedora mxe and AUR ones, do not
    # include this workaround though, so I suppose there is
    # something wrong in my environment.
    "$tardir/configure" \
      CXX="$arch-g++" \
      --prefix="/usr/$arch" \
      --host="$arch" \
      --disable-java \
      --disable-native-java \
      --disable-csharp \
      --enable-static \
      --enable-threads=win32 \
      --without-emacs \
      --disable-openmp
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/$pkgname-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    find "$prefix" -regex '.*\.\(exe\|bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip -xg {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +

    # Remove components selectively
    rm -fr "$prefix/share/doc"
    rm -fr "$prefix/share/gettext"
    rm -fr "$prefix/share/info"
    rm -fr "$prefix/share/man"
    rm -fr "$prefix/lib/gettext"
    rm -fr "$prefix/lib/gettext"
    rm -f  "$prefix/share/locale/locale.alias"
  done
}

# vim:set ts=2 sw=2 et:
