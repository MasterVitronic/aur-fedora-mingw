# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-libjpeg-turbo.git/

_realname=libjpeg-turbo
pkgname="fedora-mingw-w64-${_realname}"
pkgver=1.3.1
pkgrel=8
pkgdesc='libjpeg derivative with accelerated baseline JPEG compression and decompression (mingw-w64)'
arch=(any)
url='http://libjpeg-turbo.virtualgl.org/'
license=(GPL custom)
groups=(fedora-mingw)
depends=(mingw-w64-crt)
makedepends=(mingw-w64-gcc mingw-w64-cmake nasm)
provides=(mingw-w64-libjpeg=8.0.2)
conflicts=(mingw-w64-libjpeg mingw-w64-libjpeg-turbo)
options=(!strip staticlibs !buildflags)
source=("http://downloads.sourceforge.net/project/libjpeg-turbo/$pkgver/libjpeg-turbo-$pkgver.tar.gz"
        "http://pkgs.fedoraproject.org/cgit/mingw-libjpeg-turbo.git/plain/libjpeg-turbo-match-autoconf-behavior.patch?id=8f8b58073df47e55a74d1e62453ce17875a0d475"
        "http://pkgs.fedoraproject.org/cgit/mingw-libjpeg-turbo.git/plain/libjpeg-turbo-r1237.patch?id=f40c4581f5fc89a5b3e22b5c04c6acc3721b6439"
        "http://pkgs.fedoraproject.org/cgit/mingw-libjpeg-turbo.git/plain/libjpeg-turbo-commit-1365.patch?id=e968c5631e81a4a7fb9e267bb628675168fb48fd"
        "http://pkgs.fedoraproject.org/cgit/mingw-libjpeg-turbo.git/plain/libjpeg-turbo-commit-1367.patch?id=e968c5631e81a4a7fb9e267bb628675168fb48fd")
md5sums=('2c3a68129dac443a72815ff5bb374b05'
         '6b00863ecdd729cb757e7f3672324f6f'
         'abeaa749e4156b8e4e092b94a2723dde'
         'bc06613266b001fc1331370dee8160cb'
         'bf826d48645f0851e91f3f6d427b5d7f')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

prepare() {
  cd "$srcdir/${_realname}-$pkgver"

  # Make jconfig.h more autoconf friendly
  # https://bugzilla.redhat.com/show_bug.cgi?id=843193
  patch -Np1 < '../libjpeg-turbo-match-autoconf-behavior.patch?id=8f8b58073df47e55a74d1e62453ce17875a0d475'

  # Fix compatibility with older CMake versions (as used on RHEL7)
  patch -Np1 < '../libjpeg-turbo-r1237.patch?id=f40c4581f5fc89a5b3e22b5c04c6acc3721b6439'

  # Fix CVE-2014-9092 (RHBZ #1169851 #1169853)
  patch -Np1 < '../libjpeg-turbo-commit-1365.patch?id=e968c5631e81a4a7fb9e267bb628675168fb48fd'
  patch -Np1 < '../libjpeg-turbo-commit-1367.patch?id=e968c5631e81a4a7fb9e267bb628675168fb48fd'
}

build() {
  local tardir="$srcdir/${_realname}-$pkgver"
  local builddir
  local arch

  for arch in ${_architectures[@]}; do
    builddir="$tardir-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    $arch-cmake "$tardir"
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/${_realname}-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    find "$prefix" -regex '.*\.\(exe\|bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip --strip-unneeded {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +
    rm -fr "$prefix/doc"
  done
}

# vim:set ts=2 sw=2 et:
