# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-libjpeg-turbo.git/

_realname=libjpeg-turbo
pkgname="fedora-mingw-w64-${_realname}"
pkgver=1.4.2
pkgrel=1
pkgdesc='libjpeg derivative with accelerated baseline JPEG compression and decompression (mingw-w64)'
arch=(any)
url='http://libjpeg-turbo.virtualgl.org/'
license=(GPL custom)
groups=(aur-fedora-mingw fedora-mingw)
depends=(mingw-w64-crt)
makedepends=(mingw-w64-gcc mingw-w64-cmake nasm)
provides=(mingw-w64-libjpeg=8.0.2)
conflicts=(mingw-w64-libjpeg mingw-w64-libjpeg-turbo)
options=(!strip staticlibs !buildflags)
source=("http://downloads.sourceforge.net/project/libjpeg-turbo/$pkgver/libjpeg-turbo-$pkgver.tar.gz"
        "http://pkgs.fedoraproject.org/cgit/mingw-libjpeg-turbo.git/plain/libjpeg-turbo-match-autoconf-behavior.patch?id=e839a929eac6cce6872e3f89647eb02de85871e7")
md5sums=('86b0d5f7507c2e6c21c00219162c3c44'
         '5487be46ddbf4540dc60bd63e299dc4d')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

prepare() {
  cd "$srcdir/${_realname}-$pkgver"

  # Make jconfig.h more autoconf friendly
  # https://bugzilla.redhat.com/show_bug.cgi?id=843193
  patch -Np1 < '../libjpeg-turbo-match-autoconf-behavior.patch?id=e839a929eac6cce6872e3f89647eb02de85871e7'
}

build() {
  local tardir="$srcdir/${_realname}-$pkgver"
  local builddir
  local arch

  for arch in ${_architectures[@]}; do
    builddir="$tardir-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    $arch-cmake "$tardir"
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/${_realname}-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    find "$prefix" -regex '.*\.\(exe\|bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip --strip-unneeded {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +
    rm -fr "$prefix/doc"
  done
}

# vim:set ts=2 sw=2 et:
