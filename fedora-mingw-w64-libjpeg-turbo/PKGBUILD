# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-libjpeg-turbo.git/

pkgname=$CARCH-mingw-w64-libjpeg-turbo
pkgver=1.5.1
pkgrel=3
pkgdesc='libjpeg derivative with accelerated baseline JPEG compression and decompression (mingw-w64)'
arch=(any)
url='http://libjpeg-turbo.virtualgl.org/'
license=(GPL custom)
groups=(afm-$CARCH)
depends=(mingw-w64-crt)
makedepends=(
    mingw-w64-gcc
    mingw-w64-cmake
    nasm
)
source=("http://downloads.sourceforge.net/project/libjpeg-turbo/$pkgver/libjpeg-turbo-$pkgver.tar.gz"
        "http://pkgs.fedoraproject.org/cgit/mingw-libjpeg-turbo.git/plain/libjpeg-turbo-match-autoconf-behavior.patch?id=e839a929eac6cce6872e3f89647eb02de85871e7")
md5sums=('55deb139b0cac3c8200b75d485fc13f3'
         '5487be46ddbf4540dc60bd63e299dc4d')

prepare() {
  cd "$srcdir/libjpeg-turbo-$pkgver"

  # Make jconfig.h more autoconf friendly
  # https://bugzilla.redhat.com/show_bug.cgi?id=843193
  patch -Np1 < '../libjpeg-turbo-match-autoconf-behavior.patch?id=e839a929eac6cce6872e3f89647eb02de85871e7'
}

build() {
  local tardir="$srcdir/libjpeg-turbo-$pkgver"
  local builddir="$tardir-build"

  mkdir -p "$builddir"
  cd "$builddir"
  $CHOST-cmake "$tardir"
  make
}

package() {
  local prefix="$pkgdir/usr/$CHOST"

  cd "$srcdir/libjpeg-turbo-$pkgver-build"
  make DESTDIR="$pkgdir" install

  find "$prefix" -name '*.exe' -exec $CHOST-strip {} +
  find "$prefix" -name '*.dll' -exec $CHOST-strip --strip-unneeded {} +
  find "$prefix" -name '*.a'   -exec $CHOST-strip -g {} +
}

# vim:set ts=2 sw=2 et:
