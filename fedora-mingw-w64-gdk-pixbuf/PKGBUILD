# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-gdk-pixbuf.git/
#
# Differently from Fedora, libtiff (tiff image support) and
# jasper (JPEG2000 image support) dependencies are dropped
# and gdiplus support is not enabled.

_realname=gdk-pixbuf
pkgname="fedora-mingw-w64-${_realname}"
pkgver=2.31.1
pkgrel=3
pkgdesc='An image loading library (mingw-w64)'
arch=(any)
url='http://www.gtk.org/'
license=(LGPL)
groups=(fedora-mingw)
depends=(mingw-w64-crt fedora-mingw-w64-glib2 fedora-mingw-w64-freetype
         mingw-w64-libjpeg mingw-w64-libpng)
makedepends=(afm-mingw-w64-makedepends afm-mingw-w64-gobject-introspection wine)
provides=(mingw-w64-gdk-pixbuf2=$pkgver)
conflicts=(mingw-w64-gdk-pixbuf2)
options=(!strip staticlibs !buildflags)
source=("http://ftp.gnome.org/pub/GNOME/sources/gdk-pixbuf/${pkgver%.*}/gdk-pixbuf-$pkgver.tar.xz"
        "gdk-pixbuf.loaders")
md5sums=('74cde211f5b7ac1015d1a7c9feee037c'
         'ff9e0cc33a8f5b1889b624ab2bae3337')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

build() {
  local tardir="$srcdir/${_realname}-$pkgver"
  local builddir
  local arch

  unset LDFLAGS CPPFLAGS

  for arch in ${_architectures[@]}; do
    builddir="$tardir-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"

    # Prepare environment needed by gobject-introspection
    export PKG_CONFIG=/usr/bin/$arch-pkg-config
    export DLLTOOL=/usr/bin/$arch-dlltool
    export GI_OS_NAME=nt
    [ $arch = i686-w64-mingw32 ] && bits=32 || bits=64
    export INTROSPECTION_LAUNCHER="/usr/bin/env WINEARCH=win$bits WINEPREFIX=$srcdir/win$bits DISPLAY= /usr/bin/wine"

    "$tardir/configure" \
      --build="$CHOST" \
      --prefix="/usr/$arch" \
      --host="$arch" \
      --with-included-loaders=png \
      --without-libjasper
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/${_realname}-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    find "$prefix" -regex '.*\.\(exe\|bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip -xg {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +
    rm -fr "$prefix/share/man"
  done
}

# vim:set ts=2 sw=2 et:
