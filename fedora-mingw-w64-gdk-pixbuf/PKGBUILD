# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-gdk-pixbuf.git/
#
# Differently from Fedora, libtiff (tiff image support) and
# jasper (JPEG2000 image support) dependencies are dropped
# and gdiplus support is not enabled.

_realname=gdk-pixbuf
pkgname="fedora-mingw-w64-${_realname}"
pkgver=2.36.6
pkgrel=1
pkgdesc='An image loading library (mingw-w64)'
arch=(any)
url='http://www.gtk.org/'
license=(LGPL)
groups=(aur-fedora-mingw fedora-mingw)
depends=(mingw-w64-crt fedora-mingw-w64-glib2 fedora-mingw-w64-freetype
         mingw-w64-libjpeg mingw-w64-libpng)
makedepends=(afm-mingw-w64-makedepends mingw-w64-gobject-introspection wine)
provides=(mingw-w64-gdk-pixbuf2=$pkgver)
conflicts=(mingw-w64-gdk-pixbuf2)
options=(!strip staticlibs !buildflags)
source=("http://ftp.gnome.org/pub/GNOME/sources/${_realname}/${pkgver%.*}/${_realname}-$pkgver.tar.xz"
        # The cache file is generated with the following command:
        # wine src/gdk-pixbuf-*-build-i686-w64-mingw32/gdk-pixbuf/gdk-pixbuf-query-loaders.exe | sed 's|/usr/i686-w64-mingw32/|../|' > gdk-pixbuf.loaders
        "gdk-pixbuf.loaders")
md5sums=('5dd53760750670d27c194ff6ace7eb51'
         '371fd7546d48f1276687b6bc7b514083')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

build() {
  local tardir="$srcdir/${_realname}-$pkgver"
  local builddir
  local arch

  for arch in ${_architectures[@]}; do
    builddir="$tardir-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    source $arch-configure "$tardir" \
      --enable-relocations \
      --with-included-loaders=png \
      --without-libjasper \
      --enable-introspection

    # Copy the cache file into the source tree
    install -m 0644 "$srcdir/gdk-pixbuf.loaders" gdk-pixbuf/loaders.cache

    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/${_realname}-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    find "$prefix" -regex '.*\.\(exe\|bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip --strip-unneeded {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +

    rm -fr "$prefix/share/man"
  done
}

# vim:set ts=2 sw=2 et:
