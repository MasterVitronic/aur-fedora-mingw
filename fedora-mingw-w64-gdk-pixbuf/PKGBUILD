# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-gdk-pixbuf.git/
#
# Differently from Fedora, libtiff (tiff image support) and
# jasper (JPEG2000 image support) dependencies are dropped
# and gdiplus support is not enabled.

pkgname=$CARCH-mingw-w64-gdk-pixbuf
pkgver=2.36.7
pkgrel=3
pkgdesc='An image loading library (mingw-w64)'
arch=(any)
url='http://www.gtk.org/'
license=(LGPL)
groups=(afm-$CARCH)
depends=(
    mingw-w64-crt
    $CARCH-mingw-w64-glib2
    $CARCH-mingw-w64-freetype
    $CARCH-mingw-w64-libjpeg-turbo
    $CARCH-mingw-w64-libpng
)
makedepends=(
    afm-mingw-w64-makedepends
    $CARCH-mingw-w64-gobject-introspection
    wine
)
source=("http://ftp.gnome.org/pub/GNOME/sources/gdk-pixbuf/${pkgver%.*}/gdk-pixbuf-$pkgver.tar.xz"
        # The cache file is generated with the following command:
        # wine src/gdk-pixbuf-*-build-i686-w64-mingw32/gdk-pixbuf/gdk-pixbuf-query-loaders.exe | sed 's|/usr/i686-w64-mingw32/|../|' > gdk-pixbuf.loaders
        "gdk-pixbuf.loaders")
md5sums=('04ca1535f3fab7070db48009ed1e93f6'
         '371fd7546d48f1276687b6bc7b514083')

build() {
  local tardir="$srcdir/gdk-pixbuf-$pkgver"
  local builddir="$tardir-build"

  mkdir -p "$builddir"
  cd "$builddir"
  source $CHOST-configure "$tardir" \
    --enable-relocations \
    --with-included-loaders=png \
    --without-libjasper \
    --enable-introspection

  # Copy the loaders.cache file to the source tree
  install -m 0644 "$srcdir/gdk-pixbuf.loaders" gdk-pixbuf/loaders.cache

  make
}

package() {
  local prefix="$pkgdir/usr/$CHOST"

  cd "$srcdir/gdk-pixbuf-$pkgver-build"
  make DESTDIR="$pkgdir" install

  # Install the loaders.cache file
  install -m 0644 "$srcdir/gdk-pixbuf.loaders" "$prefix/lib/gdk-pixbuf-2.0/2.10.0/loaders.cache"

  find "$prefix" -name '*.exe' -exec $CHOST-strip {} +
  find "$prefix" -name '*.dll' -exec $CHOST-strip --strip-unneeded {} +
  find "$prefix" -name '*.a'   -exec $CHOST-strip -g {} +
}

# vim:set ts=2 sw=2 et:
