# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-gdk-pixbuf.git/
#
# Differently from Fedora, libtiff (tiff image support) and
# jasper (JPEG2000 image support) dependencies are dropped
# and gdiplus support is not enabled.

pkgname=fedora-mingw-w64-gdk-pixbuf
pkgver=2.28.2
pkgrel=4
pkgdesc='An image loading library (mingw-w64)'
arch=(any)
url='http://www.gtk.org/'
license=(LGPL)
groups=(fedora-mingw)
depends=(mingw-w64-crt fedora-mingw-w64-glib2 fedora-mingw-w64-freetype
         mingw-w64-libjpeg mingw-w64-libpng)
makedepends=(mingw-w64-gcc mingw-w64-tools mingw-w64-pkg-config
             autoconf automake libtool)
provides=("mingw-w64-gdk-pixbuf2=$pkgver")
conflicts=(mingw-w64-gdk-pixbuf2)
options=(!strip !buildflags !libtool)
source=("http://ftp.gnome.org/pub/GNOME/sources/gdk-pixbuf/${pkgver%.*}/gdk-pixbuf-$pkgver.tar.xz"
        "gdk-pixbuf.loaders")
md5sums=('04c04e5042acd85cff6c439aba1995f1'
         'ff9e0cc33a8f5b1889b624ab2bae3337')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

build() {
  local tardir="$srcdir/${pkgname#fedora-mingw-w64-}-$pkgver"
  local builddir
  local arch

  export CFLAGS='-O2 -pipe'
  export CXXFLAGS="$CFLAGS"
  unset  LDFLAGS
  unset  CPPFLAGS

  for arch in ${_architectures[@]}; do
    builddir="$srcdir/$pkgname-$pkgver-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    "$tardir/configure" \
      --prefix="/usr/$arch" \
      --host="$arch" \
      --with-included-loaders=png \
      --without-libjasper
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/$pkgname-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    find "$prefix" -regex '.*\.\(exe\|bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip -xg {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +
    rm -fr "$prefix/share/man"
  done
}

# vim:set ts=2 sw=2 et:
