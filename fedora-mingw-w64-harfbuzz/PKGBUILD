# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-harfbuzz.git/

_realname=harfbuzz
pkgname="fedora-mingw-w64-${_realname}"
pkgver=1.4.4
pkgrel=1
pkgdesc='OpenType text shaping engine (mingw-w64)'
arch=(any)
url='http://www.harfbuzz.org/'
license=(MIT)
groups=(aur-fedora-mingw fedora-mingw)
depends=(mingw-w64-crt fedora-mingw-w64-glib2 fedora-mingw-w64-freetype
         fedora-mingw-w64-cairo mingw-w64-icu)
makedepends=(afm-mingw-w64-makedepends mingw-w64-gobject-introspection wine)
provides=("mingw-w64-${_realname}=$pkgver")
conflicts=("mingw-w64-${_realname}")
options=(!strip staticlibs !buildflags)
source=("http://www.freedesktop.org/software/${_realname}/release/${_realname}-$pkgver.tar.bz2"
        "http://pkgs.fedoraproject.org/cgit/rpms/mingw-harfbuzz.git/plain/harfbuzz-enable-delayload-freetype.patch?id=ffe49919d88f9980d197c48066eefd67d3ac072e")
md5sums=('ddac65a48b1d2c6dda8bddf1f37faeb3'
         'fcd307058298447861570ad68415d7e0')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

prepare() {
  cd "$srcdir/${_realname}-$pkgver"

  # Make freetype an optional runtime dependency instead of a hard dependency (using delay load)
  patch -Np1 < '../harfbuzz-enable-delayload-freetype.patch?id=ffe49919d88f9980d197c48066eefd67d3ac072e'

  autoreconf -ivf
}

build() {
  local tardir="$srcdir/${_realname}-$pkgver"
  local builddir
  local arch

  for arch in ${_architectures[@]}; do
    builddir="$tardir-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    source $arch-configure "$tardir" \
      --enable-static \
      --enable-shared \
      --disable-silent-rules \
      --with-gobject \
      --enable-introspection
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/${_realname}-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    find "$prefix" -regex '.*\.\(exe\|bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip --strip-unneeded {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +
  done
}

# vim:set ts=2 sw=2 et:
