# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-harfbuzz.git/

pkgname=fedora-mingw-w64-harfbuzz
pkgver=0.9.14
pkgrel=2
pkgdesc='OpenType text shaping engine (mingw-w64)'
arch=(any)
url='http://www.harfbuzz.org/'
license=(MIT)
groups=(fedora-mingw)
depends=(mingw-w64-crt fedora-mingw-w64-glib2 fedora-mingw-w64-freetype
         fedora-mingw-w64-cairo mingw-w64-icu)
makedepends=(mingw-w64-gcc mingw-w64-pkg-config autoconf automake libtool)
provides=("mingw-w64-harfbuzz=$pkgver")
conflicts=(mingw-w64-harfbuzz)
options=(!strip !buildflags !libtool)
source=("http://www.freedesktop.org/software/harfbuzz/release/harfbuzz-$pkgver.tar.bz2"
        "http://pkgs.fedoraproject.org/cgit/mingw-harfbuzz.git/plain/GetProcAddress_Script_OpenType.diff?id=00afde08e2825864ad3c5a27c0b2783a3ec27cc0"
        "http://pkgs.fedoraproject.org/cgit/mingw-harfbuzz.git/plain/harfbuzz-0.9.9-fix-build-failure.patch?id=00afde08e2825864ad3c5a27c0b2783a3ec27cc0")
md5sums=('7e1990b79060e98e2d31f677a0ac9eed'
         'dc3c8db194835ad113d2994a76b17edb'
         '39a89824f77cc7979b4025379b38aae4')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

build() {
  local tardir="$srcdir/${pkgname#fedora-mingw-w64-}-$pkgver"
  local builddir
  local arch

  # Apply patches from fedora repository
  cd "$tardir"
  patch -Np1 < '../GetProcAddress_Script_OpenType.diff?id=00afde08e2825864ad3c5a27c0b2783a3ec27cc0'
  patch -Np1 < '../harfbuzz-0.9.9-fix-build-failure.patch?id=00afde08e2825864ad3c5a27c0b2783a3ec27cc0'

  # The libtool bundled with the tarball is too old for win64 support
  cd "$tardir"
  autoreconf -if
  libtoolize -cf

  export CFLAGS='-O2 -pipe'
  export CXXFLAGS="$CFLAGS"
  unset  LDFLAGS
  unset  CPPFLAGS

  for arch in ${_architectures[@]}; do
    builddir="$srcdir/$pkgname-$pkgver-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    "$tardir/configure" \
      --prefix="/usr/$arch" \
      --host="$arch" \
      --enable-static \
      --enable-shared
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/$pkgname-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    find "$prefix" -regex '.*\.\(exe\|bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip -xg {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +
    rm -fr "$prefix/share"
  done
}

# vim:set ts=2 sw=2 et:
