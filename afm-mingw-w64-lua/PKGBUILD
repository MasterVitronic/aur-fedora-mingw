# Maintainer: Nicola Fontana <ntd@entidi.it>
# Maintainer: Alexey Pavlov <alexpux@gmail.com>
#
# This is an adaptation of the MinGW package by Alexpux found on GitHub:
# https://github.com/Alexpux/MINGW-packages/tree/master/mingw-w64-lua

_realname=lua
pkgname="afm-mingw-w64-${_realname}"
pkgver=5.2.4
pkgrel=3
pkgdesc='Powerful light-weight programming language designed for extending applications (mingw-w64)'
arch=(any)
url='http://www.lua.org/'
license=(MIT)
depends=(mingw-w64-crt)
makedepends=(mingw-w64-gcc)
provides=("mingw-w64-lua=$pkgver")
conflicts=(mingw-w64-lua)
options=(!strip staticlibs !buildflags)
source=("http://www.lua.org/ftp/${_realname}-$pkgver.tar.gz"
        "lua.pc")
md5sums=('913fdb32207046b273fdb17aad70be13'
         'e7ba6c2b695b0b84a5ea0cbff5fc9067')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

build() {
  local tardir="$srcdir/${_realname}-$pkgver"
  local builddir
  local arch
  local prefix

  unset CPPFLAGS CFLAGS LDFLAGS

  for arch in ${_architectures[@]}; do
    builddir="$tardir-build-$arch"
    prefix="$pkgdir/usr/$arch"

    # Lua does not support VPATH builds
    cp -fr "$tardir" "$builddir"
    sed -e "s|%VER%|${pkgver%.*}|g" \
        -e "s|%REL%|$pkgver|g" \
        -e "s|/usr|/usr/$arch|g" \
        "$srcdir/lua.pc" > "$builddir/lua.pc"

    cd "$builddir"
    make -j1 \
      MYCFLAGS="-Wp,-D_FORTIFY_SOURCE=2 --param=ssp-buffer-size=4" \
      AR="$arch-ar rcu" \
      RANLIB="$arch-ranlib" \
      STRIP="$arch-strip" \
      CC="$arch-gcc" \
      mingw
  done
}

package() {
  local builddir
  local arch
  local prefix

  unset CPPFLAGS CFLAGS LDFLAGS

  for arch in ${_architectures[@]}; do
    builddir="$srcdir/${_realname}-$pkgver-build-$arch"
    prefix="$pkgdir/usr/$arch"

    cd "$builddir"
    make \
      INSTALL_TOP="$prefix" \
      TO_BIN="lua.exe luac.exe lua52.dll" \
      install

    # Manually install the pkg-config file
    install -Dm644 "$builddir/lua.pc" "$prefix/lib/pkgconfig/lua${pkgver%.*}.pc"
    # This symlink should be done only for the current Lua release
    ln -s "lua${pkgver%.*}.pc" "$prefix/lib/pkgconfig/lua.pc"
  done
}

# vim:set ts=2 sw=2 et:
