# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-freetype.git/

pkgname=$CARCH-mingw-w64-freetype
pkgver=2.7.1
pkgrel=3
pkgdesc='TrueType font rendering library (mingw-w64)'
arch=(any)
url='http://freetype.sourceforge.net/'
license=(GPL)
groups=(afm-$CARCH)
depends=(
    mingw-w64-crt
    $CARCH-mingw-w64-zlib
    $CARCH-mingw-w64-bzip2
)
makedepends=(
    afm-mingw-w64-makedepends
    $CARCH-mingw-w64-libpng
)
source=("http://downloads.sourceforge.net/sourceforge/freetype/freetype-$pkgver.tar.bz2")
md5sums=('b3230110e0cab777e0df7631837ac36e')

build() {
  local tardir="$srcdir/freetype-$pkgver"
  local builddir="$tardir-build"

  mkdir -p "$builddir"
  cd "$builddir"
  # FreeType mimics the typical autotools behavior
  # but it is *not* autotools based
  "$tardir/configure" \
    --build="$BUILDHOST" \
    --host="$CHOST" \
    --prefix="/usr/$CHOST" \
    --enable-static \
    --enable-shared \
    LIBPNG_CFLAGS="$(/usr/$CHOST/bin/libpng-config --cflags)" \
    LIBPNG_LDFLAGS="$(/usr/$CHOST/bin/libpng-config --ldflags)"
  make
}

package() {
  local prefix="$pkgdir/usr/$CHOST"

  cd "$srcdir/freetype-$pkgver-build"
  make DESTDIR="$pkgdir" install

  find "$prefix" -name '*.exe' -exec $CHOST-strip {} +
  find "$prefix" -name '*.dll' -exec $CHOST-strip --strip-unneeded {} +
  find "$prefix" -name '*.a'   -exec $CHOST-strip -g {} +

  rm -f  "$prefix/bin/freetype-config"
}

# vim:set ts=2 sw=2 et:
