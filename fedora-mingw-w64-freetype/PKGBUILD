# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-freetype.git/

pkgname=fedora-mingw-w64-freetype
pkgver=2.4.11
pkgrel=5
pkgdesc='TrueType font rendering library (mingw-w64)'
arch=(any)
url='http://freetype.sourceforge.net/'
license=(GPL)
groups=(fedora-mingw)
depends=(mingw-w64-crt mingw-w64-zlib mingw-w64-bzip2)
makedepends=(mingw-w64-gcc)
provides=("mingw-w64-freetype=$pkgver")
conflicts=(mingw-w64-freetype)
replaces=()
options=(!strip !buildflags !libtool)
source=("http://downloads.sourceforge.net/sourceforge/freetype/freetype-$pkgver.tar.bz2"
        "http://pkgs.fedoraproject.org/cgit/mingw-freetype.git/plain/freetype-2.3.0-enable-spr.patch?h=f18"
        "http://pkgs.fedoraproject.org/cgit/mingw-freetype.git/plain/freetype-2.2.1-enable-valid.patch?h=f18"
        "http://pkgs.fedoraproject.org/cgit/mingw-freetype.git/plain/freetype-multilib.patch?h=f18")
md5sums=('b93435488942486c8d0ca22e8f768034'
         '816dc8619a6904a7385769433c0a8653'
         '214119610444c9b02766ccee5e220680'
         '3bb0a2e25ed0a74e7218b8e82103c415')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

build() {
  local tardir="$srcdir/${pkgname#fedora-mingw-w64-}-$pkgver"
  local builddir
  local arch

  # Apply patches from fedora repository
  cd "$tardir"
  patch -Np1 < '../freetype-2.3.0-enable-spr.patch?h=f18'
  patch -Np1 < '../freetype-2.2.1-enable-valid.patch?h=f18'
  patch -Np1 < '../freetype-multilib.patch?h=f18'

  export CFLAGS='-O2 -pipe'
  export CXXFLAGS="$CFLAGS"
  unset  LDFLAGS
  unset  CPPFLAGS

  for arch in ${_architectures[@]}; do
    builddir="$srcdir/$pkgname-$pkgver-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    "$tardir/configure" \
      --prefix="/usr/$arch" \
      --build="$CHOST" \
      --host="$arch" \
      --enable-static \
      --enable-shared
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/$pkgname-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    find "$prefix" -regex '.*\.\(exe\|bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip -xg {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +
    rm -f  "$prefix/bin/freetype-config"
  done
}

# vim:set ts=2 sw=2 et:
