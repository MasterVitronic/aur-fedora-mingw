# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-pixman.git/

pkgname=fedora-mingw-w64-pixman
pkgver=0.32.0
pkgrel=2
pkgdesc='The pixel-manipulation library for X and cairo (mingw-w64)'
arch=(any)
url='http://pixman.org/'
license=(LGPL MPL custom)
groups=(fedora-mingw)
depends=(mingw-w64-crt)
# pkg-config is not used because of --disable-gtk but is checked anyway
makedepends=(mingw-w64-gcc mingw-w64-pkg-config)
provides=(mingw-w64-pixman=$pkgver)
conflicts=(mingw-w64-pixman)
options=(!strip staticlibs !buildflags)
source=("http://cairographics.org/releases/pixman-$pkgver.tar.gz")
md5sums=('0f7ee566a7dfb0260be11f3e944a708c')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

build() {
  local tardir="$srcdir/${pkgname#fedora-mingw-w64-}-$pkgver"
  local builddir
  local arch

  unset LDFLAGS CPPFLAGS

  for arch in ${_architectures[@]}; do
    builddir="$srcdir/$pkgname-$pkgver-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    "$tardir/configure" \
      --build="$CHOST" \
      --prefix="/usr/$arch" \
      --host="$arch" \
      --disable-sse2 \
      --disable-gtk \
      --enable-shared \
      --enable-static
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/$pkgname-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    find "$prefix" -regex '.*\.\(exe\|bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip -xg {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +
  done
}

# vim:set ts=2 sw=2 et:
