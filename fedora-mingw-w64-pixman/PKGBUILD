# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-pixman.git/

pkgname=$CARCH-mingw-w64-pixman
pkgver=0.34.0
pkgrel=4
pkgdesc='The pixel-manipulation library for X and cairo (mingw-w64)'
arch=(any)
url='http://pixman.org/'
license=(LGPL MPL custom)
groups=(afm-$CARCH)
depends=(mingw-w64-crt)
makedepends=(afm-mingw-w64-makedepends)
source=("http://cairographics.org/releases/pixman-$pkgver.tar.gz")
md5sums=('e80ebae4da01e77f68744319f01d52a3')

build() {
  local tardir="$srcdir/pixman-$pkgver"
  local builddir="$tardir-build"

  mkdir -p "$builddir"
  cd "$builddir"
  source $CHOST-configure "$tardir" \
    --enable-static \
    --enable-shared \
    --disable-silent-rules \
    --disable-sse2 \
    --disable-gtk
  make
}

package() {
  local prefix="$pkgdir/usr/$CHOST"

  cd "$srcdir/pixman-$pkgver-build"
  make DESTDIR="$pkgdir" install

  find "$prefix" -name '*.exe' -exec $CHOST-strip {} +
  find "$prefix" -name '*.dll' -exec $CHOST-strip --strip-unneeded {} +
  find "$prefix" -name '*.a'   -exec $CHOST-strip -g {} +
}

# vim:set ts=2 sw=2 et:
