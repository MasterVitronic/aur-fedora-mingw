# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-pixman.git/

pkgname=fedora-mingw-w64-pixman
pkgver=0.28.0
pkgrel=4
pkgdesc='The pixel-manipulation library for X and cairo (mingw-w64)'
arch=(any)
url='http://pixman.org/'
license=(LGPL MPL)
license=(custom)
groups=(fedora-mingw)
depends=(mingw-w64-crt)
# pkg-config is not used because of --disable-gtk but is checked anyway
makedepends=(mingw-w64-gcc mingw-w64-pkg-config)
provides=("mingw-w64-pixman=$pkgver")
conflicts=(mingw-w64-pixman)
options=(!strip !buildflags !libtool)
source=("http://cairographics.org/releases/pixman-$pkgver.tar.gz")
md5sums=('0554c354aed2d7845180f310a9a15f20')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

build() {
  local tardir="$srcdir/${pkgname#fedora-mingw-w64-}-$pkgver"
  local builddir
  local arch

  export CFLAGS='-O2 -pipe'
  export CXXFLAGS="$CFLAGS"
  unset  LDFLAGS
  unset  CPPFLAGS

  for arch in ${_architectures[@]}; do
    builddir="$srcdir/$pkgname-$pkgver-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    "$tardir/configure" \
      --prefix="/usr/$arch" \
      --host="$arch" \
      --disable-gtk \
      --enable-shared \
      --enable-static
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/$pkgname-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    find "$prefix" -regex '.*\.\(exe\|bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip -xg {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +
  done
}

# vim:set ts=2 sw=2 et:
