# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-atk.git/

pkgname=$CARCH-mingw-w64-atk
pkgver=2.24.0
pkgrel=2
pkgdesc='A library providing a set of interfaces for accessibility (mingw-w64)'
arch=(any)
url='http://www.gtk.org/'
license=(LGPL)
groups=(afm-$CARCH)
depends=(
    mingw-w64-crt
    $CARCH-mingw-w64-win-iconv
    $CARCH-mingw-w64-gettext
    $CARCH-mingw-w64-glib2
)
makedepends=(
    afm-mingw-w64-makedepends
    $CARCH-mingw-w64-gobject-introspection
    wine
)
source=("http://ftp.gnome.org/pub/GNOME/sources/atk/${pkgver%.*}/atk-$pkgver.tar.xz")
md5sums=('3747a80089dfa3c0bbcf21adfff9968b')

build() {
  local tardir="$srcdir/atk-$pkgver"
  local builddir="$tardir-build"

  mkdir -p "$builddir"
  cd "$builddir"
  source $CHOST-configure "$tardir" \
    --enable-static \
    --enable-shared \
    --disable-silent-rules \
    --enable-introspection
  make
}

package() {
  local prefix="$pkgdir/usr/$CHOST"

  cd "$srcdir/atk-$pkgver-build"
  make DESTDIR="$pkgdir" install

  find "$prefix" -name '*.exe' -exec $CHOST-strip {} +
  find "$prefix" -name '*.dll' -exec $CHOST-strip --strip-unneeded {} +
  find "$prefix" -name '*.a'   -exec $CHOST-strip -g {} +
}

# vim:set ts=2 sw=2 et:
