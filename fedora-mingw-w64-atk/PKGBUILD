# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-atk.git/

pkgname=fedora-mingw-w64-atk
pkgver=2.6.0
pkgrel=3
pkgdesc='A library providing a set of interfaces for accessibility (mingw-w64)'
arch=(any)
url='http://www.gtk.org/'
license=(LGPL)
groups=(fedora-mingw)
depends=(mingw-w64-crt fedora-mingw-w64-win-iconv fedora-mingw-w64-gettext fedora-mingw-w64-glib2)
makedepends=('mingw-w64-gcc>=4.7.0' mingw-w64-pkg-config)
provides=("mingw-w64-atk=$pkgver")
conflicts=(mingw-w64-atk)
options=(!strip !buildflags !libtool)
source=("http://ftp.gnome.org/pub/GNOME/sources/atk/${pkgver%.*}/atk-$pkgver.tar.xz")
md5sums=('6b34e2a63dda4429b5692be7ca3aa5bf')

_architectures=(i686-w64-mingw32
                x86_64-w64-mingw32)

build() {
  local tardir="$srcdir/${pkgname#fedora-mingw-w64-}-$pkgver"
  local builddir
  local arch

  # I have to do this otherwise win64 build fails
  # (the Fedora package seems to not need it).
  cd $tardir
  autoreconf -if

  export CFLAGS='-O2 -pipe'
  export CXXFLAGS="$CFLAGS"
  unset  LDFLAGS

  for arch in ${_architectures[@]}; do
    builddir="$srcdir/$pkgname-$pkgver-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    "$tardir/configure" \
      --prefix="/usr/$arch" \
      --host="$arch" \
      --enable-shared \
      --enable-static
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/$pkgname-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    find "$prefix" -regex '.*\.\(exe\|bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip -xg {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +
    rm -fr "$prefix/share"
  done
}

# vim:set ts=2 sw=2 et:
