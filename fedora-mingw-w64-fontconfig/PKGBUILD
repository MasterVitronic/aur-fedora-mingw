# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-fontconfig.git/

pkgname=fedora-mingw-w64-fontconfig
pkgver=2.10.91
pkgrel=5
pkgdesc='A library for configuring and customizing font access (mingw-w64)'
arch=(any)
url='http://fontconfig.org/'
license=(custom)
groups=(fedora-mingw)
depends=(mingw-w64-crt fedora-mingw-w64-expat fedora-mingw-w64-freetype fedora-mingw-w64-win-iconv mingw-w64-libxml2)
makedepends=(mingw-w64-gcc mingw-w64-pkg-config)
provides=("mingw-w64-fontconfig=$pkgver")
conflicts=(mingw-w64-fontconfig)
options=(!strip !buildflags !libtool)
source=("http://www.fontconfig.org/release/fontconfig-$pkgver.tar.bz2")
md5sums=('c795bb39fab3a656e5dff8bad6a199f6')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

build() {
  local tardir="$srcdir/${pkgname#fedora-mingw-w64-}-$pkgver"
  local builddir
  local arch

  export CFLAGS='-O2 -pipe'
  export CXXFLAGS="$CFLAGS"
  unset  LDFLAGS
  unset  CPPFLAGS

  for arch in ${_architectures[@]}; do
    builddir="$srcdir/$pkgname-$pkgver-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    # Setting cache-dir to a known directory helps to remove it later.
    # No idea why other build systems do not consider this issue.
    "$tardir/configure" \
      --prefix="/usr/$arch" \
      --host="$arch" \
      --with-arch="${arch%%-*}" \
      --with-cache-dir="/tmp" \
      --disable-docs
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/$pkgname-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    # Executable files must be preseved for cache management on the host
    find "$prefix" -regex '.*\.\(bat\|def\|exp\)' -exec rm -f           {} +
    find "$prefix" -name '*.exe'                  -exec $arch-strip     {} +
    find "$prefix" -name '*.dll'                  -exec $arch-strip -xg {} +
    find "$prefix" -name '*.a'                    -exec $arch-strip -g  {} +
    rm -fr "$prefix/share/man"
  done

  rm -fr "$pkgdir/tmp"
}

# vim:set ts=2 sw=2 et:
