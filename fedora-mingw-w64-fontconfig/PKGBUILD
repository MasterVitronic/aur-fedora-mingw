# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-fontconfig.git/

pkgname=$CARCH-mingw-w64-fontconfig
pkgver=2.12.1
pkgrel=4
pkgdesc='A library for configuring and customizing font access (mingw-w64)'
arch=(any)
url='http://fontconfig.org/'
license=(custom)
groups=(afm-$CARCH)
depends=(
    mingw-w64-crt
    $CARCH-mingw-w64-expat
    $CARCH-mingw-w64-freetype
    $CARCH-mingw-w64-win-iconv
)
makedepends=(
    afm-mingw-w64-makedepends
    python
)
source=("http://www.fontconfig.org/release/fontconfig-$pkgver.tar.bz2")
md5sums=('b5af5a423ee3b5cfc34846838963c058')

build() {
  local tardir="$srcdir/fontconfig-$pkgver"
  local builddir="$tardir-build"

  mkdir -p "$builddir"
  cd "$builddir"
  source $CHOST-configure "$tardir" \
    --enable-static \
    --enable-shared \
    --with-arch="$CARCH" \
    --disable-docs
  make
}

package() {
  local prefix="$pkgdir/usr/$CHOST"

  cd "$srcdir/fontconfig-$pkgver-build"
  make DESTDIR="$pkgdir" install

  find "$prefix" -name '*.exe' -exec $CHOST-strip {} +
  find "$prefix" -name '*.dll' -exec $CHOST-strip --strip-unneeded {} +
  find "$prefix" -name '*.a'   -exec $CHOST-strip -g {} +

  # The eventual cache dir does not invalidate the final package,
  # so leaving it untouched for debugging purposes.
  #rm -fr "$pkgdir/../${pkgname}WINDOWSTEMPDIR_FONTCONFIG_CACHE"
}

# vim:set ts=2 sw=2 et:
