# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-fontconfig.git/

pkgname=fedora-mingw-w64-fontconfig
pkgver=2.11.0
pkgrel=3
pkgdesc='A library for configuring and customizing font access (mingw-w64)'
arch=(any)
url='http://fontconfig.org/'
license=(custom)
groups=(fedora-mingw)
depends=(mingw-w64-crt mingw-w64-expat mingw-w64-freetype mingw-w64-win-iconv)
makedepends=(mingw-w64-gcc mingw-w64-pkg-config)
provides=(mingw-w64-fontconfig=$pkgver)
conflicts=(mingw-w64-fontconfig)
replaces=()
options=(!strip staticlibs !buildflags)
source=("http://www.fontconfig.org/release/fontconfig-$pkgver.tar.bz2"
        "http://pkgs.fedoraproject.org/cgit/mingw-fontconfig.git/plain/fontconfig-disable-migration-testcase-on-win32.patch?id=332b9f475053aba063d99c5aa1fc8a7486e95131")
md5sums=('000bd4baf7aefa828e03414d0c8c7dc5'
         'a10fef13b7cc1159ea32e939123c3980')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

build() {
  local tardir="$srcdir/${pkgname#fedora-mingw-w64-}-$pkgver"
  local builddir
  local arch

  # Apply patches from fedora repository
  cd "$tardir"

  # The testcase called 'migration' relies too much on UNIX features
  # like chmod, so disable the testcase when cross-compiling for win32
  patch -Np1 < '../fontconfig-disable-migration-testcase-on-win32.patch?id=332b9f475053aba063d99c5aa1fc8a7486e95131'

  autoreconf -if

  unset LDFLAGS CPPFLAGS

  for arch in ${_architectures[@]}; do
    builddir="$srcdir/$pkgname-$pkgver-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    "$tardir/configure" \
      --build="$CHOST" \
      --prefix="/usr/$arch" \
      --host="$arch" \
      --with-arch="${arch%%-*}" \
      --disable-docs
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/$pkgname-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    # Executable files must be preseved for cache management on the host
    find "$prefix" -regex '.*\.\(bat\|def\|exp\)' -exec rm -f           {} +
    find "$prefix" -name '*.exe'                  -exec $arch-strip     {} +
    find "$prefix" -name '*.dll'                  -exec $arch-strip -xg {} +
    find "$prefix" -name '*.a'                    -exec $arch-strip -g  {} +
    rm -fr "$prefix/share/man"
  done

  # The eventual cache dir does not invalidate the final package,
  # so leaving it untouched for debugging purposes.
  #rm -fr "$pkgdir/../${pkgname}WINDOWSTEMPDIR_FONTCONFIG_CACHE"
}

# vim:set ts=2 sw=2 et:
