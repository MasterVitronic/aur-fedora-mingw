# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-fontconfig.git/

_realname=fontconfig
pkgname="fedora-mingw-w64-${_realname}"
pkgver=2.12.1
pkgrel=2
pkgdesc='A library for configuring and customizing font access (mingw-w64)'
arch=(any)
url='http://fontconfig.org/'
license=(custom)
groups=(aur-fedora-mingw fedora-mingw)
depends=(mingw-w64-crt mingw-w64-expat mingw-w64-freetype mingw-w64-win-iconv)
makedepends=(afm-mingw-w64-makedepends python)
provides=("mingw-w64-${_realname}=$pkgver")
conflicts=("mingw-w64-${_realname}")
options=(!strip staticlibs !buildflags)
source=("http://www.fontconfig.org/release/${_realname}-$pkgver.tar.bz2")
md5sums=('b5af5a423ee3b5cfc34846838963c058')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

build() {
  local tardir="$srcdir/${_realname}-$pkgver"
  local builddir
  local arch

  for arch in ${_architectures[@]}; do
    builddir="$tardir-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    source $arch-configure "$tardir" \
      --enable-static \
      --enable-shared \
      --with-arch="${arch%%-*}" \
      --disable-docs
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/${_realname}-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    # Executable files must be preseved for cache management on the host
    find "$prefix" -regex '.*\.\(bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.exe' -exec $arch-strip {} +
    find "$prefix" -name '*.dll' -exec $arch-strip --strip-unneeded {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +

    rm -fr "$prefix/share/man"
  done

  # The eventual cache dir does not invalidate the final package,
  # so leaving it untouched for debugging purposes.
  #rm -fr "$pkgdir/../${pkgname}WINDOWSTEMPDIR_FONTCONFIG_CACHE"
}

# vim:set ts=2 sw=2 et:
