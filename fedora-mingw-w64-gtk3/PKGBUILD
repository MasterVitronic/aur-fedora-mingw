# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-gtk3.git/

pkgname=fedora-mingw-w64-gtk3
pkgver=3.9.0
pkgrel=1
pkgdesc='GTK+3 is a multi-platform toolkit (mingw-w64)'
arch=(any)
url='http://www.gtk.org/'
license=(LGPL)
groups=(fedora-mingw)
depends=(mingw-w64-crt fedora-mingw-w64-atk fedora-mingw-w64-cairo
         fedora-mingw-w64-gdk-pixbuf fedora-mingw-w64-gettext
         fedora-mingw-w64-glib2 fedora-mingw-w64-win-iconv
         fedora-mingw-w64-pango fedora-mingw-w64-pixman mingw-w64-zlib)
makedepends=(mingw-w64-gcc mingw-w64-pkg-config glib2
             autoconf automake libtool gtk-doc gobject-introspection)
provides=("mingw-w64-gtk3=$pkgver")
conflicts=(mingw-w64-gtk3)
options=(!strip !buildflags !libtool)
source=("http://ftp.gnome.org/pub/GNOME/sources/gtk+/${pkgver%.*}/gtk+-$pkgver.tar.xz"
        "http://pkgs.fedoraproject.org/cgit/mingw-gtk3.git/plain/0003-Don-t-use-AC_CANONICAL_TARGET.patch?id=b63e31724f2786202a38f8b7fb8539b009f4e621"
        "http://pkgs.fedoraproject.org/cgit/mingw-gtk3.git/plain/gtk-dont-define-initguid.patch?id=82ccf489f4763e375805d848351ac3f8fda8e88b"
        "http://pkgs.fedoraproject.org/cgit/mingw-gtk3.git/plain/commit-914099dc.patch?id=3fa3354330e47e5b9de898579c010ffd00f9de2f"
        "gtk.immodules")
md5sums=('23310b5a9d1489f40ee688c5a0ea67d0'
         '77b0525b67057fa6afaf5405d80509ae'
         '4038939df90f80ea6923d67afff28e03'
         '7fb133de82f410ca48aae51ca57dcdba'
         '9655591f6ed7b9371dc7c15f8237d383')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

build() {
  # gtk3 != gtk+, so hardcoding it in $tardir
  local tardir="$srcdir/gtk+-$pkgver"
  local builddir
  local arch

  # Apply patches from fedora repository
  cd "$tardir"
  patch -Np1 < '../0003-Don-t-use-AC_CANONICAL_TARGET.patch?id=b63e31724f2786202a38f8b7fb8539b009f4e621'
  # As of mingw-w64 r5589 the header knownfolders.h was added
  # which contains definitions like FOLDERID_Windows
  # As these are also part of libuuid.a you'll get linker failures like:
  # /usr/i686-w64-mingw32/sys-root/mingw/lib/libuuid.a(lib32_libuuid_a-uuid.o):(.rdata+0x0): multiple definition of `FOLDERID_Windows'
  # win32/.libs/libgdk-win32.a(gdkdnd-win32.o):gdkdnd-win32.c:(.rdata+0x360): first defined here
  #
  # Workaround this for now until mingw-w64 upstream has come up
  # with a statement whether this is expected behaviour and what
  # component should be fixed exactly
  #
  # http://mingw-w64.svn.sourceforge.net/viewvc/mingw-w64?view=revision&revision=5589
  patch -Np0 < '../gtk-dont-define-initguid.patch?id=82ccf489f4763e375805d848351ac3f8fda8e88b'
  # https://bugzilla.gnome.org/show_bug.cgi?id=699690
  patch -Np1 < '../commit-914099dc.patch?id=3fa3354330e47e5b9de898579c010ffd00f9de2f'

  autoreconf -if

  export CFLAGS='-O2 -pipe'
  export CXXFLAGS="$CFLAGS"
  unset  LDFLAGS
  unset  CPPFLAGS

  for arch in ${_architectures[@]}; do
    builddir="$srcdir/$pkgname-$pkgver-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    "$tardir/configure" \
      --prefix="/usr/$arch" \
      --build="$CHOST" \
      --host="$arch" \
      --enable-gtk2-dependency \
      --disable-cups
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/$pkgname-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    # Do not remove exe files (leave gtk3-demo.exe for testing purpose)
    find "$prefix" -regex '.*\.\(bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip -xg {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +

    # Compile the basic schemas, a requirement to have working
    # GtkFileChooser (and possibly GtkColorChooser) widgets.
    glib-compile-schemas "$prefix/share/glib-2.0/schemas"
  done
}

# vim:set ts=2 sw=2 et:
