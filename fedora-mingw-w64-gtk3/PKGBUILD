# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-gtk3.git/

pkgname=fedora-mingw-w64-gtk3
pkgver=3.14.3
pkgrel=1
pkgdesc='GTK+3 is a multi-platform toolkit (mingw-w64)'
arch=(any)
url='http://www.gtk.org/'
license=(LGPL)
groups=(fedora-mingw)
depends=(mingw-w64-crt fedora-mingw-w64-atk fedora-mingw-w64-cairo
         fedora-mingw-w64-gdk-pixbuf fedora-mingw-w64-gettext
         fedora-mingw-w64-glib2 fedora-mingw-w64-win-iconv
         fedora-mingw-w64-pango fedora-mingw-w64-pixman mingw-w64-zlib)
makedepends=(mingw-w64-gcc mingw-w64-pkg-config
             glib2 gobject-introspection)
# Upstream packaging has gone wrong:
#
# make[2]: Entering directory `/builddir/build/BUILD/gtk+-3.9.8/build_win32/gdk'
# cd ../.. && /bin/sh /builddir/build/BUILD/gtk+-3.9.8/build-aux/missing automake-1.13 --gnu gdk/Makefile
# /builddir/build/BUILD/gtk+-3.9.8/build-aux/missing: line 81: automake-1.13: command not found
# WARNING: 'automake-1.13' is missing on your system.
#
# Workaround this for now
makedepends+=(autoconf automake libtool)
provides=(mingw-w64-gtk3=$pkgver)
conflicts=(mingw-w64-gtk3)
options=(!strip staticlibs !buildflags)
source=("http://ftp.gnome.org/pub/GNOME/sources/gtk+/${pkgver%.*}/gtk+-$pkgver.tar.xz"
        "http://pkgs.fedoraproject.org/cgit/mingw-gtk3.git/plain/gtk.immodules?id=3028da05cb7e088b4477a5b30262c7c4ca1e7937"
        "http://pkgs.fedoraproject.org/cgit/mingw-gtk3.git/plain/gtk3-bug-731013-fix-cross-compilation.patch?id=d734837aa460b213b4a68819ebc56d265dd50425")
md5sums=('17fd226c03d95347b1aef3b138c2a8c9'
         'df8ad196a274f9e0d91bfb0078a0f9bd'
         '7e245b38b9e1d2bb6682922cabd11f64')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

build() {
  # gtk3 != gtk+, so hardcoding it in $tardir
  local tardir="$srcdir/gtk+-$pkgver"
  local builddir
  local arch

  cd "$tardir"

  # Cross-compilation is broken as of gtk+ 3.13.2
  # Apply a work-in-progress patch to fix compilation
  # https://bugzilla.gnome.org/show_bug.cgi?id=731013
  patch -Np1 < '../gtk3-bug-731013-fix-cross-compilation.patch?id=d734837aa460b213b4a68819ebc56d265dd50425'

  autoreconf -if

  unset LDFLAGS CPPFLAGS
  for arch in ${_architectures[@]}; do
    builddir="$srcdir/$pkgname-$pkgver-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    "$tardir/configure" \
      --build="$CHOST" \
      --prefix="/usr/$arch" \
      --host="$arch" \
      --enable-gtk2-dependency \
      --disable-cups \
      --enable-static \
      --disable-gtk-doc
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/$pkgname-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    # Install the gtk.immodules file
    install -m 0644 '../gtk.immodules?id=3028da05cb7e088b4477a5b30262c7c4ca1e7937' $prefix/etc/gtk-3.0/gtk.immodules

    # Do not remove exe files (leave gtk3-demo.exe for testing purpose)
    find "$prefix" -regex '.*\.\(bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip -xg {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +

    # Remove desktop files and corresponding icons as they aren't
    # useful for win32
    rm -f "$prefix"/share/applications/*.desktop
    rm -f "$prefix"/share/icons/hicolor/*/apps/*.png

    # Compile the basic schemas, a requirement to have working
    # GtkFileChooser (and possibly GtkColorChooser) widgets.
    glib-compile-schemas "$prefix/share/glib-2.0/schemas"
  done
}

# vim:set ts=2 sw=2 et:
