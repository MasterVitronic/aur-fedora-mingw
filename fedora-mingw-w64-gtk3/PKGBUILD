# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-gtk3.git/

pkgname=fedora-mingw-w64-gtk3
pkgver=3.7.14
pkgrel=1
pkgdesc='GTK+3 is a multi-platform toolkit (mingw-w64)'
arch=(any)
url='http://www.gtk.org/'
license=(LGPL)
groups=(fedora-mingw)
depends=(mingw-w64-crt fedora-mingw-w64-atk fedora-mingw-w64-cairo
         fedora-mingw-w64-gdk-pixbuf fedora-mingw-w64-gettext
         fedora-mingw-w64-glib2 fedora-mingw-w64-win-iconv
         fedora-mingw-w64-pango fedora-mingw-w64-pixman mingw-w64-zlib)
makedepends=('mingw-w64-gcc>=4.7.0' mingw-w64-pkg-config glib2
             autoconf automake libtool gtk-doc gobject-introspection)
provides=("mingw-w64-gtk3=$pkgver")
conflicts=(mingw-w64-gtk3)
options=(!strip !buildflags !libtool)
source=("http://ftp.gnome.org/pub/GNOME/sources/gtk+/${pkgver%.*}/gtk+-$pkgver.tar.xz"
        "http://pkgs.fedoraproject.org/cgit/mingw-gtk3.git/plain/commit-a2cd99.patch?id=14aeddfdf4bdd95b4dba78e99b91475b78fde3e7"
        "http://pkgs.fedoraproject.org/cgit/mingw-gtk3.git/plain/0003-Don-t-use-AC_CANONICAL_TARGET.patch?id=b63e31724f2786202a38f8b7fb8539b009f4e621"
        "gtk.immodules")

md5sums=('6171e8c9513a9ae639d660231ca6dbed'
         'a7175512f725d2cf82cc78d5cbcf9899'
         '77b0525b67057fa6afaf5405d80509ae'
         '9655591f6ed7b9371dc7c15f8237d383')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

build() {
  # gtk3 != gtk+, so hardcoding it in $tardir
  local tardir="$srcdir/gtk+-$pkgver"
  local builddir
  local arch

  # Apply patches from fedora repository
  cd "$tardir"
  # Link with -lwinmm
  # https://git.gnome.org/browse/gtk+/commit/?id=a2cd9983c3e017238a9e0fa41674171a9adacdc4
  patch -Np1 < '../commit-a2cd99.patch?id=14aeddfdf4bdd95b4dba78e99b91475b78fde3e7'
  patch -Np1 < '../0003-Don-t-use-AC_CANONICAL_TARGET.patch?id=b63e31724f2786202a38f8b7fb8539b009f4e621'
  autoreconf -if

  export CFLAGS='-O2 -pipe'
  export CXXFLAGS="$CFLAGS"
  unset  LDFLAGS

  for arch in ${_architectures[@]}; do
    builddir="$srcdir/$pkgname-$pkgver-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    "$tardir/configure" \
      --prefix="/usr/$arch" \
      --host="$arch" \
      --enable-gtk2-dependency \
      --disable-cups
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/$pkgname-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    # Do not remove exe files (leave gtk3-demo.exe for testing purpose)
    find "$prefix" -regex '.*\.\(bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip -xg {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +

    # Compile the basic schemas, a requirement to have working
    # GtkFileChooser (and possibly GtkColorChooser) widgets.
    glib-compile-schemas "$prefix/share/glib-2.0/schemas"
  done
}

# vim:set ts=2 sw=2 et:
