# Maintainer: Nicola Fontana <ntd@entidi.it>

_realname=lua-lgi
_luaver=5.2
pkgname="afm-mingw-w64-${_realname}"
pkgver=0.8.0
pkgrel=3
pkgdesc='Lua bindings for gnome/gobject using gobject-introspection library (mingw-w64)'
arch=(any)
url='http://www.lua.org/'
license=(MIT)
depends=(mingw-w64-crt mingw-w64-lua mingw-w64-gobject-introspection)
makedepends=(afm-mingw-w64-makedepends)
options=(!strip staticlibs !buildflags)
source=("${_realname}-$pkgver.tar.gz::https://github.com/pavouk/lgi/archive/$pkgver.tar.gz"
        "0001-Allow-overriding-of-pkg-config-executable.patch"
        "0002-Export-luaopen_lgi_corelgilua51.patch"
        "0003-Extend-platform-support-to-MSYS-and-MinGW.patch"
        "0004-Allow-overriding-of-the-host-build-system.patch")
md5sums=('f0a161fc07b65afabdd09aa5455390a3'
         'e6f1496707e8ccc6edba95f5bc77774a'
         '4eeb84f885ba8f85964dd8a5b25105ac'
         'a6660fa55a07da149b42653fea85f2e4'
         '600b8100486070a877dc948eec168977')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

prepare() {
  local tardir="$srcdir/${_realname}-$pkgver"

  # Use $_realname instead of "lgi"
  rm -fr "$tardir"
  cp -fr "$srcdir/lgi-$pkgver" "$tardir"
  cd "$tardir"

  # The first patch does not apply cleanly because it is rebased on
  # HEAD. Ignoring the error though, because the failing chunk is inside
  # tests/Makefile (that it is not run)
  patch -Np1 < ../0001-Allow-overriding-of-pkg-config-executable.patch || :
  patch -Np1 < ../0002-Export-luaopen_lgi_corelgilua51.patch
  patch -Np1 < ../0003-Extend-platform-support-to-MSYS-and-MinGW.patch
  patch -Np1 < ../0004-Allow-overriding-of-the-host-build-system.patch
}

build() {
  local tardir="$srcdir/${_realname}-$pkgver"
  local builddir
  local arch
  local deps="gobject-introspection-1.0 gmodule-2.0 libffi"

  unset LDFLAGS CPPFLAGS

  for arch in ${_architectures[@]}; do
    builddir="$tardir-build-$arch"
    # lua-lgi does not support VPATH builds
    cp -fr "$tardir" "$builddir"
    cd "$builddir"
    # Do not use pkg-config to get the Lua libraries because the result
    # will be suitable only for static linking
    make \
      HOST_OS=mingw32 \
      CFLAGS="$($arch-pkg-config --cflags lua${_luaver} $deps)" \
      LIBS="-L/usr/$arch/bin -llua${_luaver/./} $($arch-pkg-config --libs $deps)" \
      LUA_VERSION="${_luaver}" \
      CC="$arch-gcc" \
      PKG_CONFIG="$arch-pkg-config"
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/${_realname}-$pkgver-build-$arch"
    make \
      HOST_OS=mingw32 \
      LUA_VERSION="${_luaver}" \
      PREFIX="/usr/$arch" \
      DESTDIR="$pkgdir/" install

    find "$prefix" -name '*.dll' -exec $arch-strip -xg {} +
  done
}

# vim:set ts=2 sw=2 et:
