# Maintainer: Nicola Fontana <ntd@entidi.it>

_realname=lua-lgi
_luaver=5.2
pkgname="afm-mingw-w64-${_realname}"
pkgver=0.9.0
pkgrel=2
pkgdesc='Lua bindings for gnome/gobject using gobject-introspection library (mingw-w64)'
arch=(any)
url='http://www.lua.org/'
license=(MIT)
depends=(mingw-w64-crt mingw-w64-lua mingw-w64-gobject-introspection)
makedepends=(afm-mingw-w64-makedepends)
options=(!strip staticlibs !buildflags)
source=("${_realname}-$pkgver.tar.gz::https://github.com/pavouk/lgi/archive/$pkgver.tar.gz"
        "0001-Allow-overriding-of-pkg-config-executable.patch"
        "0002-Export-luaopen_lgi_corelgilua51.patch"
        "0003-Extend-platform-support-to-MSYS-and-MinGW.patch"
        "0004-Allow-overriding-of-the-host-build-system.patch"
        "0001-Do-not-copy-version.lua-twice.patch"
        "Disable-GDK-lock.patch")
md5sums=('cc433a597f23cfabdfc905c6c2cd3d7c'
         'e6f1496707e8ccc6edba95f5bc77774a'
         '4eeb84f885ba8f85964dd8a5b25105ac'
         'a6660fa55a07da149b42653fea85f2e4'
         '600b8100486070a877dc948eec168977'
         '7688457a774d8ca306d4f59655887c9b'
         '3de38470e6238a2b190a9f54c551f74e')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

prepare() {
  local tardir="$srcdir/${_realname}-$pkgver"

  # Use $_realname instead of "lgi"
  rm -fr "$tardir"
  cp -fr "$srcdir/lgi-$pkgver" "$tardir"
  cd "$tardir"

  # The first patch does not apply cleanly because it is rebased on
  # HEAD. Ignoring the error though, because the failing chunk is inside
  # tests/Makefile (that it is not run)
  patch -Np1 < ../0001-Allow-overriding-of-pkg-config-executable.patch || :
  patch -Np1 < ../0002-Export-luaopen_lgi_corelgilua51.patch
  patch -Np1 < ../0003-Extend-platform-support-to-MSYS-and-MinGW.patch
  patch -Np1 < ../0004-Allow-overriding-of-the-host-build-system.patch
  patch -Np1 < ../0001-Do-not-copy-version.lua-twice.patch

  # Temporary disable the GDK lock automatically operated by lgi:
  # https://github.com/pavouk/lgi/issues/98
  patch -Np1 < ../Disable-GDK-lock.patch
}

build() {
  local tardir="$srcdir/${_realname}-$pkgver"
  local builddir
  local arch
  local deps="lua${_luaver} gobject-introspection-1.0 gmodule-2.0 libffi"

  for arch in ${_architectures[@]}; do
    builddir="$tardir-build-$arch"
    # lua-lgi does not support VPATH builds
    cp -fr "$tardir" "$builddir"
    cd "$builddir"
    # -no-undefined seems to be required for Windows, see e.g.
    # https://sourceware.org/autobook/autobook/autobook_253.html#SEC253
    make \
      HOST_OS=mingw32 \
      COPTFLAGS="-O2 -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 --param=ssp-buffer-size=4" \
      LDFLAGS=-Wl,-no-undefined \
      CFLAGS="$($arch-pkg-config --cflags $deps)" \
      LIBS="$($arch-pkg-config --libs $deps)" \
      LUA_VERSION="${_luaver}" \
      CC="$arch-gcc" \
      PKG_CONFIG="$arch-pkg-config"
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/${_realname}-$pkgver-build-$arch"
    make \
      HOST_OS=mingw32 \
      LUA_VERSION="${_luaver}" \
      PREFIX="/usr/$arch" \
      DESTDIR="$pkgdir/" \
      install

    find "$prefix" -name '*.dll' -exec $arch-strip --strip-unneeded {} +
  done
}

# vim:set ts=2 sw=2 et:
