# Maintainer: Nicola Fontana <ntd@entidi.it>

_luaver=5.2

pkgname=$CARCH-mingw-w64-lua-lgi
pkgver=0.9.0
pkgrel=6
pkgdesc='Lua bindings for gnome/gobject using gobject-introspection library (mingw-w64)'
arch=(any)
url='http://www.lua.org/'
license=(MIT)
groups=(afm-$CARCH)
depends=(
    mingw-w64-crt
    $CARCH-mingw-w64-lua
    $CARCH-mingw-w64-glib2
    $CARCH-mingw-w64-gobject-introspection
)
makedepends=(afm-mingw-w64-makedepends)
source=("lua-lgi-$pkgver.tar.gz::https://github.com/pavouk/lgi/archive/$pkgver.tar.gz"
        "0001-Allow-overriding-of-pkg-config-executable.patch"
        "0002-Export-luaopen_lgi_corelgilua51.patch"
        "0003-Extend-platform-support-to-MSYS-and-MinGW.patch"
        "0004-Allow-overriding-of-the-host-build-system.patch"
        "0001-Do-not-copy-version.lua-twice.patch"
        "Disable-GDK-lock.patch")
md5sums=('cc433a597f23cfabdfc905c6c2cd3d7c'
         'e6f1496707e8ccc6edba95f5bc77774a'
         '4eeb84f885ba8f85964dd8a5b25105ac'
         'a6660fa55a07da149b42653fea85f2e4'
         '600b8100486070a877dc948eec168977'
         '7688457a774d8ca306d4f59655887c9b'
         '3de38470e6238a2b190a9f54c551f74e')

prepare() {
  local tardir="$srcdir/lgi-$pkgver"

  cd "$tardir"

  # The first patch does not apply cleanly because it is rebased on
  # HEAD. Ignoring the error though, because the failing chunk is inside
  # tests/Makefile (that it is not run)
  patch -Np1 < ../0001-Allow-overriding-of-pkg-config-executable.patch || :
  patch -Np1 < ../0002-Export-luaopen_lgi_corelgilua51.patch
  patch -Np1 < ../0003-Extend-platform-support-to-MSYS-and-MinGW.patch
  patch -Np1 < ../0004-Allow-overriding-of-the-host-build-system.patch
  patch -Np1 < ../0001-Do-not-copy-version.lua-twice.patch

  # Temporary disable the GDK lock automatically operated by lgi:
  # https://github.com/pavouk/lgi/issues/98
  patch -Np1 < ../Disable-GDK-lock.patch
}

build() {
  local tardir="$srcdir/lgi-$pkgver"
  local builddir="$tardir-build"
  local deps="lua${_luaver} gobject-introspection-1.0 gmodule-2.0 libffi"

  # lua-lgi does not support VPATH builds
  cp -fr "$tardir" "$builddir"
  cd "$builddir"
  make \
    HOST_OS=mingw32 \
    COPTFLAGS="$CFLAGS" \
    LDFLAGS="$LDFLAGS" \
    CFLAGS="$($CHOST-pkg-config --cflags $deps)" \
    LIBS="$($CHOST-pkg-config --libs $deps)" \
    LUA_VERSION="${_luaver}" \
    CC="$CHOST-gcc" \
    PKG_CONFIG="$CHOST-pkg-config"
}

package() {
  local prefix="$pkgdir/usr/$CHOST"

  cd "$srcdir/lgi-$pkgver-build"
  make \
    HOST_OS=mingw32 \
    LUA_VERSION="${_luaver}" \
    PREFIX="/usr/$CHOST" \
    DESTDIR="$pkgdir/" \
    install

  find "$prefix" -name '*.exe' -exec $CHOST-strip {} +
  find "$prefix" -name '*.dll' -exec $CHOST-strip --strip-unneeded {} +
  find "$prefix" -name '*.a'   -exec $CHOST-strip -g {} +
}

# vim:set ts=2 sw=2 et:
