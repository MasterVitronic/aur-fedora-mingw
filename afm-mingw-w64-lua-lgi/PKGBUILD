# Maintainer: Nicola Fontana <ntd@entidi.it>

_luaver=5.3

pkgname=$CARCH-mingw-w64-lua-lgi
pkgver=0.9.1
pkgrel=3
pkgdesc='Lua bindings for gnome/gobject using gobject-introspection library (mingw-w64)'
arch=(any)
url='http://www.lua.org/'
license=(MIT)
groups=(afm-$CARCH)
depends=(
    mingw-w64-crt
    $CARCH-mingw-w64-lua
    $CARCH-mingw-w64-glib2
    $CARCH-mingw-w64-gobject-introspection
)
makedepends=(afm-mingw-w64-makedepends)
source=("lua-lgi-$pkgver.tar.gz::https://github.com/pavouk/lgi/archive/$pkgver.tar.gz"
        "22374789ed5ee9621a7fb7042462bf6a51ff0a8b.patch")
md5sums=('0c85f177e4b8192bd849b4e508c8bdf0'
         '49781a1a7ef6a5c99a5c1ce75e3966ec')

prepare() {
  local tardir="$srcdir/lgi-$pkgver"

  cd "$tardir"

  # Don't hard-code the pkg-config executable
  patch -Np1 < ../22374789ed5ee9621a7fb7042462bf6a51ff0a8b.patch
}

build() {
  local tardir="$srcdir/lgi-$pkgver"
  local builddir="$tardir-build"
  local deps="lua${_luaver} gobject-introspection-1.0 gmodule-2.0 libffi"

  # lua-lgi does not support VPATH builds
  cp -fr "$tardir" "$builddir"
  cd "$builddir"
  make \
    HOST_OS=mingw32 \
    COPTFLAGS="$CFLAGS" \
    LDFLAGS="$LDFLAGS" \
    CFLAGS="$($CHOST-pkg-config --cflags $deps)" \
    LIBS="$($CHOST-pkg-config --libs $deps)" \
    LUA_VERSION="${_luaver}" \
    CC="$CHOST-gcc" \
    PKG_CONFIG="$CHOST-pkg-config"
}

package() {
  local prefix="$pkgdir/usr/$CHOST"

  cd "$srcdir/lgi-$pkgver-build"
  make \
    HOST_OS=mingw32 \
    LUA_VERSION="${_luaver}" \
    PREFIX="/usr/$CHOST" \
    DESTDIR="$pkgdir/" \
    install

  find "$prefix" -name '*.exe' -exec $CHOST-strip {} +
  find "$prefix" -name '*.dll' -exec $CHOST-strip --strip-unneeded {} +
  find "$prefix" -name '*.a'   -exec $CHOST-strip -g {} +
}

# vim:set ts=2 sw=2 et:
