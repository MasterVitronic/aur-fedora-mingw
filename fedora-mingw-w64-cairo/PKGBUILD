# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-cairo.git/

pkgname=fedora-mingw-w64-cairo
pkgver=1.12.6
pkgrel=1
pkgdesc='Cairo vector graphics library (mingw-w64)'
arch=(any)
url='http://cairographics.org/'
license=(LGPL MPL)
groups=(fedora-mingw)
depends=(mingw-w64-crt fedora-mingw-w64-fontconfig fedora-mingw-w64-freetype
         mingw-w64-libxml2 fedora-mingw-w64-pixman mingw-w64-libpng
         fedora-mingw-w64-win-iconv mingw-w64-zlib fedora-mingw-w64-glib2)
makedepends=('mingw-w64-gcc>=4.7.0' mingw-w64-tools mingw-w64-pkg-config)
provides=("mingw-w64-cairo=$pkgver")
conflicts=(mingw-w64-cairo)
options=(!strip !buildflags !libtool)
source=("http://cairographics.org/releases/cairo-$pkgver.tar.xz")
sha1sums=('a383c6cb4495e18848ea43e1031c294aa9417a43')

_architectures=(i686-w64-mingw32
                x86_64-w64-mingw32)

build() {
  local tardir="$srcdir/${pkgname#fedora-mingw-w64-}-$pkgver"
  local builddir
  local arch

  # Function to generate an import library for delay-loading
  gen_delay_lib() {
    local DLL="$1"
    local OUT_IMPLIB="$2"

    gendef - "$DLL" > delay_load.def
    $arch-dlltool --def delay_load.def --kill-at --output-delaylib "$OUT_IMPLIB"
    rm -f delay_load.def
  }

  export CFLAGS='-O2 -pipe'
  export CXXFLAGS="$CFLAGS"
  unset  LDFLAGS

  for arch in ${_architectures[@]}; do
    builddir="$srcdir/$pkgname-$pkgver-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"

    # Generate import libs for delay-loading fontconfig and freetype
    gen_delay_lib "/usr/$arch/bin/libfontconfig-1.dll" libfontconfig-delayed.dll.a
    gen_delay_lib "/usr/$arch/bin/libfreetype-6.dll" libfreetype-delayed.dll.a

    "$tardir/configure" \
      --prefix="/usr/$arch" \
      --host="$arch" \
      --disable-pthread \
      --disable-xlib \
      --disable-xcb \
      --enable-win32 \
      --enable-fc \
      --enable-ft \
      --enable-png \
      --enable-static \
      --enable-gobject \
      --enable-tee \
      FONTCONFIG_LIBS="-L$builddir -lfontconfig-delayed" \
      FREETYPE_LIBS="-L$builddir -lfreetype-delayed"
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/$pkgname-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    find "$prefix" -regex '.*\.\(exe\|bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip -xg {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +
    rm -fr "$prefix/share"
  done
}

# vim:set ts=2 sw=2 et:
