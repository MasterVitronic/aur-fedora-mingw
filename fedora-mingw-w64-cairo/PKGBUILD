# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-cairo.git/

_realname=cairo
pkgname="fedora-mingw-w64-${_realname}"
pkgver=1.14.0
pkgrel=5
pkgdesc='Cairo vector graphics library (mingw-w64)'
arch=(any)
url='http://cairographics.org/'
license=(LGPL MPL)
groups=(fedora-mingw)
depends=(mingw-w64-crt fedora-mingw-w64-fontconfig fedora-mingw-w64-freetype
         mingw-w64-libxml2 fedora-mingw-w64-pixman mingw-w64-libpng
         fedora-mingw-w64-win-iconv mingw-w64-zlib fedora-mingw-w64-glib2)
makedepends=(afm-mingw-w64-makedepends mingw-w64-tools)
provides=("mingw-w64-${_realname}=$pkgver")
conflicts=("mingw-w64-${_realname}")
options=(!strip staticlibs !buildflags)
source=("http://cairographics.org/releases/cairo-$pkgver.tar.xz")
md5sums=('fc3a5edeba703f906f2241b394f0cced')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

build() {
  local tardir="$srcdir/${_realname}-$pkgver"
  local builddir
  local arch

  # Function to generate an import library for delay-loading
  gen_delay_lib() {
    local DLL="$1"
    local OUT_IMPLIB="$2"

    gendef - "$DLL" > delay_load.def
    $arch-dlltool --def delay_load.def --kill-at --output-delaylib "$OUT_IMPLIB"
    rm -f delay_load.def
  }

  for arch in ${_architectures[@]}; do
    builddir="$tardir-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"

    # Generate import libs for delay-loading fontconfig and freetype
    gen_delay_lib "/usr/$arch/bin/libfontconfig-1.dll" libfontconfig-delayed.dll.a
    gen_delay_lib "/usr/$arch/bin/libfreetype-6.dll" libfreetype-delayed.dll.a

    ax_cv_c_float_words_bigendian=no \
    source $arch-configure "$tardir" \
      --enable-static \
      --enable-shared \
      --disable-silent-rules \
      --disable-pthread \
      --disable-xlib \
      --disable-xcb \
      --enable-win32 \
      --enable-fc \
      --enable-ft \
      --enable-png \
      --enable-gobject \
      --enable-tee \
      FONTCONFIG_LIBS="-L$builddir -lfontconfig-delayed" \
      FREETYPE_LIBS="-L$builddir -lfreetype-delayed"
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/${_realname}-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    find "$prefix" -regex '.*\.\(exe\|bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip --strip-unneeded {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +

    rm -fr "$prefix/share"
  done
}

# vim:set ts=2 sw=2 et:
