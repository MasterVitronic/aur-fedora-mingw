# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-binutils.git/

pkgname=fedora-mingw-w64-binutils
pkgver=2.23.52.0.1
pkgrel=2
pkgdesc='A set of programs to assemble and manipulate binary and object files (mingw-w64)'
arch=(any)
url='http://www.gnu.org/software/binutils/'
license=(GPL LGPL)
groups=(fedora-mingw)
depends=(zlib)
makedepends=(flex bison texinfo)
provides=("mingw-w64-binutils=$pkgver")
conflicts=(mingw-w64-binutils)
options=(!libtool)
source=("http://www.kernel.org/pub/linux/devel/binutils/binutils-$pkgver.tar.xz"
        "http://pkgs.fedoraproject.org/cgit/mingw-binutils.git/plain/bfd_doc.txt?id=8ce795d81dcaac71998a10cf292728de746871c9"
        "http://pkgs.fedoraproject.org/cgit/mingw-binutils.git/plain/binutils-2.23.52.0.1-as-doc-texinfo-fixes.patch?id=8ce795d81dcaac71998a10cf292728de746871c9")
md5sums=('d7f4b386c8849ed35149a8e5b72252ae'
         '992112665ac574cdd811973930b35177'
         '9636000adcbffdd5ec5e2a33191b2b04')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

build() {
  local tardir="$srcdir/${pkgname#fedora-mingw-w64-}-$pkgver"
  local builddir
  local arch

  # Apply patches from fedora repository
  cd "$tardir"
  # Resolve build failure on PPC
  # Contributed by Kai Tietz
  patch -Np0 < '../bfd_doc.txt?id=8ce795d81dcaac71998a10cf292728de746871c9'
  # Fix compatibility with latest texinfo
  # Patch taken from the Fedora binutils package
  patch -Np0 < '../binutils-2.23.52.0.1-as-doc-texinfo-fixes.patch?id=8ce795d81dcaac71998a10cf292728de746871c9'

  export CFLAGS='-O2 -pipe'
  export CXXFLAGS="$CFLAGS"
  unset  LDFLAGS
  unset  CPPFLAGS

  for arch in ${_architectures[@]}; do
    builddir="$srcdir/$pkgname-$pkgver-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    # Use a custom makeinfo to force generation of doc
    # also in presence of errors: gas docs and texinfo-5
    # do not fit together.
    "$tardir/configure" \
      --prefix=/usr \
      --target="$arch" \
      --disable-nls \
      MAKEINFO='makeinfo --force --no-warn'
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"
    cd "$srcdir/$pkgname-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install
  done

  rm -fr "$pkgdir/usr/lib"
  rm -fr "$pkgdir/usr/share"
}

# vim:set ts=2 sw=2 et:
