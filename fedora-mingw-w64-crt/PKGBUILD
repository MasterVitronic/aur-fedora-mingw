# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-crt.git/

pkgname=fedora-mingw-w64-crt
pkgver=6469
pkgrel=1
pkgdesc='MinGW-w64 CRT for Windows'
arch=(any)
url='http://mingw-w64.sourceforge.net/'
license=('custom')
groups=('fedora-mingw')
depends=('mingw-w64-binutils')
makedepends=('mingw-w64-headers' 'mingw-w64-gcc'
             svn autoconf automake libtool)
optdepends=()
provides=(mingw-w64-crt)
conflicts=()
replaces=()
backup=()
options=(!strip !buildflags !libtool)
source=()
md5sums=()

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)
_svnmod='mingw-w64'
_svntrunk='svn://svn.code.sf.net/p/mingw-w64/code/trunk'

build() {
  local svndir="$srcdir/$_svnmod"
  local builddir
  local arch

  if [[ -d "$svndir/.svn" ]]; then
    cd "$_svnmod"
    svn up -r "$pkgver"
    svn revert -R .
  else
    cd "$srcdir"
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod" && cd "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"

  autoreconf -if

  export CFLAGS='-O2 -pipe'
  export CXXFLAGS="$CFLAGS"
  unset  LDFLAGS
  unset  CPPFLAGS

  for arch in ${_architectures[@]}; do
    builddir="$srcdir/$pkgname-$pkgver-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    "$svndir/mingw-w64-crt/configure" \
      --prefix="/usr/$arch" \
      --host="$arch"
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/$pkgname-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    find "$prefix" -regex '.*\.\(exe\|bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip -xg {} +
    # libruntime.a is not a library: the following workaround will let
    # strip the other libraries without failing at libruntime.a
    find "$prefix" -name '*.a' -exec $arch-strip -g {} + 2> /dev/null || :
    rm -fr "$prefix/share"
  done
}

# vim:set ts=2 sw=2 et:
