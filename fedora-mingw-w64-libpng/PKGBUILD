# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-libpng.git/

pkgname=$CARCH-mingw-w64-libpng
pkgver=1.6.29
pkgrel=2
pkgdesc='A collection of routines used to create PNG format graphics (mingw-w64)'
arch=(any)
url='http://www.libpng.org/pub/png/libpng.html'
license=(custom)
groups=(afm-$CARCH)
depends=(
    mingw-w64-crt
    $CARCH-mingw-w64-zlib
)
makedepends=(afm-mingw-w64-makedepends)
source=("http://downloads.sourceforge.net/sourceforge/libpng/libpng-$pkgver.tar.xz")
md5sums=('3245dbd76ea91e1437507357b858ec97')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

build() {
  local tardir="$srcdir/libpng-$pkgver"
  local builddir="$tardir-build"

  mkdir -p "$builddir"
  cd "$builddir"
  source $CHOST-configure "$tardir"
  make
}

package() {
  local prefix="$pkgdir/usr/$CHOST"

  cd "$srcdir/libpng-$pkgver-build"
  make DESTDIR="$pkgdir" install

  find "$prefix" -name '*.exe' -exec $CHOST-strip {} +
  find "$prefix" -name '*.dll' -exec $CHOST-strip --strip-unneeded {} +
  find "$prefix" -name '*.a'   -exec $CHOST-strip -g {} +
}

# vim:set ts=2 sw=2 et:
