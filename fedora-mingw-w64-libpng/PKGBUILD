# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-libpng.git/

_realname=libpng
pkgname="fedora-mingw-w64-${_realname}"
pkgver=1.6.21
pkgrel=1
pkgdesc='A collection of routines used to create PNG format graphics (mingw-w64)'
arch=(any)
url='http://www.libpng.org/pub/png/libpng.html'
license=(custom)
groups=(fedora-mingw)
depends=(mingw-w64-crt mingw-w64-zlib)
makedepends=(afm-mingw-w64-makedepends)
provides=(mingw-w64-libpng=$pkgver)
conflicts=(mingw-w64-libpng)
options=(!strip staticlibs !buildflags)
source=("http://downloads.sourceforge.net/sourceforge/${_realname}/${_realname}-$pkgver.tar.xz")
md5sums=('3bacb4728f6694a64ad9052769d6a4ce')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

build() {
  local tardir="$srcdir/${_realname}-$pkgver"
  local builddir
  local arch

  for arch in ${_architectures[@]}; do
    builddir="$tardir-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    source $arch-configure "$tardir"
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/${_realname}-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    find "$prefix" -regex '.*\.\(exe\|bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip --strip-unneeded {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +

    rm -fr "$prefix/share"
  done
}

# vim:set ts=2 sw=2 et:
