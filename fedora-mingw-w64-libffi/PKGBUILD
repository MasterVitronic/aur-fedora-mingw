# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-libffi.git/

_realname=libffi
pkgname="fedora-mingw-w64-${_realname}"
pkgver=3.0.13
pkgrel=11
pkgdesc='A portable, high level programming interface to various calling conventions (mingw-w64)'
arch=(any)
url='http://sourceware.org/libffi/'
license=(MIT)
groups=(aur-fedora-mingw fedora-mingw)
depends=(mingw-w64-crt)
makedepends=(mingw-w64-gcc)
provides=(mingw-w64-libffi=$pkgver)
conflicts=(mingw-w64-libffi)
options=(!strip staticlibs !buildflags)
source=("ftp://sourceware.org/pub/libffi/libffi-$pkgver.tar.gz")
md5sums=('45f3b6dbc9ee7c7dfbbbc5feba571529')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

build() {
  local tardir="$srcdir/${_realname}-$pkgver"
  local builddir
  local arch

  for arch in ${_architectures[@]}; do
    builddir="$tardir-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    "$tardir/configure" \
      --build="$CHOST" \
      --prefix="/usr/$arch" \
      --host="$arch" \
      --enable-shared
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/${_realname}-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    find "$prefix" -regex '.*\.\(exe\|bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip --strip-unneeded {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +

    rm -fr "$prefix/share"
  done
}

# vim:set ts=2 sw=2 et:
