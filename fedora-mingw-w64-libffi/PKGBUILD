# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-libffi.git/

pkgname=fedora-mingw-w64-libffi
pkgver=3.0.11
pkgrel=2
pkgdesc='A portable, high level programming interface to various calling conventions (mingw-w64)'
arch=(any)
url='http://sourceware.org/libffi/'
license=(MIT)
groups=(fedora-mingw)
depends=(mingw-w64-crt)
makedepends=('mingw-w64-gcc>=4.7.0')
provides=("mingw-w64-libffi=$pkgver")
conflicts=(mingw-w64-libffi)
options=(!strip !buildflags !libtool)
source=("ftp://sourceware.org/pub/libffi/libffi-$pkgver.tar.gz"
        "http://pkgs.fedoraproject.org/cgit/mingw-libffi.git/plain/libffi-mingw-w64-compatibility.patch?h=f18")
md5sums=('f69b9693227d976835b4857b1ba7d0e3'
         'fc9bff7e45690d609d4fef11e5b4fa3f')

_architectures=(i686-w64-mingw32
                x86_64-w64-mingw32)

build() {
  local tardir="$srcdir/${pkgname#fedora-mingw-w64-}-$pkgver"
  local builddir
  local arch

  # Apply patches from fedora repository
  cd "$tardir"
  patch -p0 < '../libffi-mingw-w64-compatibility.patch?h=f18'

  export CFLAGS='-O2 -pipe'
  export CXXFLAGS="$CFLAGS"
  unset  LDFLAGS

  for arch in ${_architectures[@]}; do
    builddir="$srcdir/$pkgname-$pkgver-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    "$tardir/configure" \
      --prefix="/usr/$arch" \
      --host="$arch" \
      --enable-shared
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/$pkgname-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    find "$prefix" -regex '.*\.\(exe\|bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip -xg {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +
    rm -fr "$prefix/share"
  done
}

# vim:set ts=2 sw=2 et:
