# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-pthreads/

pkgname=fedora-mingw-w64-pthreads
pkgver=2.8.0
pkgrel=2
_prerelease_date=20110511
pkgdesc='MinGW-w64 pthreads library'
arch=(any)
url='http://sourceware.org/pthreads-win32/'
license=(LGPL)
groups=(fedora-mingw)
depends=()
makedepends=('fedora-mingw-w64-headers' 'mingw-w64-gcc'
             'mingw-w64-binutils' 'mingw-w64-crt')
provides=(mingw-w64-pthreads)
conflicts=()
replaces=(mingw-w64-pthreads mingw-w64-winpthreads)
options=(!strip !buildflags !libtool)
source=("http://pkgs.fedoraproject.org/repo/pkgs/mingw32-pthreads/pthreads-w32-$_prerelease_date.tar.bz2/30200c77d2042bdb479a1c8cf6a44471/pthreads-w32-$_prerelease_date.tar.bz2"
        "http://pkgs.fedoraproject.org/cgit/mingw-pthreads.git/plain/mingw32-pthreads-2.8.0-20110511-no-failing-tests.patch?id=5b9069c334ab34007650489869344ae6b73296db"
        "http://pkgs.fedoraproject.org/cgit/mingw-pthreads.git/plain/mingw32-pthreads-2.8.0-20110511-use-wine-for-tests.patch?id=5b9069c334ab34007650489869344ae6b73296db"
        "http://pkgs.fedoraproject.org/cgit/mingw-pthreads.git/plain/mingw32-pthreads-flags.patch?id=5b9069c334ab34007650489869344ae6b73296db")
md5sums=('30200c77d2042bdb479a1c8cf6a44471'
         'f95940870ad03a9447c9d049ad78d110'
         '8fd397178a270e38d816a45ba917f36b'
         'a13189af1cc44f0eb5602a3c6bd247cc')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

build() {
  local tardir="$srcdir/pthreads-w32-$_prerelease_date"
  local builddir
  local arch

  # Apply patches from fedora repository
  cd "$tardir"
  patch -Np1 < '../mingw32-pthreads-2.8.0-20110511-no-failing-tests.patch?id=5b9069c334ab34007650489869344ae6b73296db'
  patch -Np1 < '../mingw32-pthreads-2.8.0-20110511-use-wine-for-tests.patch?id=5b9069c334ab34007650489869344ae6b73296db'
  patch -Np1 < '../mingw32-pthreads-flags.patch?id=5b9069c334ab34007650489869344ae6b73296db'

  export CFLAGS='-O2 -pipe'
  export CXXFLAGS="$CFLAGS"
  unset  LDFLAGS
  unset  CPPFLAGS

  for arch in ${_architectures[@]}; do
    builddir="$srcdir/$pkgname-$pkgver-build-$arch"
    cd $tardir
    cp -Rv "$tardir" "$builddir"
    cd "$builddir"

    make clean
    make CROSS="$arch-" GC-inlined
    make clean
    make CROSS="$arch-" GCE-inlined
    mv libpthreadGC2.a libpthreadGC2.dll.a
    mv libpthreadGCE2.a libpthreadGCE2.dll.a
    make clean
    make CROSS="$arch-" GC-static
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/$pkgname-$pkgver-build-$arch"

    install -d "$prefix/bin" "$prefix/lib" "$prefix/include"
    install -t "$prefix/bin"     -m 0755 *.dll
    install -t "$prefix/lib"     -m 0644 *.a
    install -t "$prefix/include" -m 0644 pthread.h sched.h semaphore.h

    # Create a symlink from libpthreadGC2.a to libpthread.a because of BZ #498616
    ln -s libpthreadGC2.a       "$prefix/lib/libpthread.a"
    ln -s libpthreadGC2.dll.a   "$prefix/lib/libpthread.dll.a"
  done
}

# vim:set ts=2 sw=2 et:
