# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-win-iconv.git/

pkgname=$CARCH-mingw-w64-win-iconv
pkgver=0.0.6
pkgrel=12
pkgdesc='Iconv implementation using Win32 API (mingw-w64)'
arch=(any)
url='http://code.google.com/p/win-iconv/'
license=(BSD)
groups=(afm-$CARCH)
depends=(mingw-w64-crt)
makedepends=(afm-mingw-w64-makedepends cmake)
source=("https://github.com/win-iconv/win-iconv/archive/$pkgver.tar.gz")
md5sums=('0bed0bf66dd3f87416dd7393ee079d01')

build() {
  local tardir="$srcdir/win-iconv-$pkgver"
  local builddir="$tardir-build"

  mkdir -p "$builddir"
  cd "$builddir"
  $CHOST-cmake -DBUILD_STATIC=1 "$tardir"
  make
}

package() {
  local prefix="$pkgdir/usr/$CHOST"

  cd "$srcdir/win-iconv-$pkgver-build"
  make DESTDIR="$pkgdir" install

  find "$prefix" -name '*.exe' -exec $CHOST-strip {} +
  find "$prefix" -name '*.dll' -exec $CHOST-strip --strip-unneeded {} +
  find "$prefix" -name '*.a'   -exec $CHOST-strip -g {} +
}

# vim:set ts=2 sw=2 et:
