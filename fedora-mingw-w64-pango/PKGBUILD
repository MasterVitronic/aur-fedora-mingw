# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-pango.git/

pkgname=fedora-mingw-w64-pango
pkgver=1.36.3
pkgrel=2
pkgdesc='A library for layout and rendering of text (mingw-w64)'
arch=(any)
url='http://www.pango.org/'
license=(LGPL)
groups=(fedora-mingw)
depends=(mingw-w64-crt fedora-mingw-w64-cairo fedora-mingw-w64-expat
         fedora-mingw-w64-fontconfig fedora-mingw-w64-freetype
         fedora-mingw-w64-gettext fedora-mingw-w64-glib2
         fedora-mingw-w64-win-iconv mingw-w64-libpng
         fedora-mingw-w64-pixman fedora-mingw-w64-harfbuzz)
makedepends=(mingw-w64-gcc mingw-w64-tools mingw-w64-pkg-config
             autoconf automake libtool gtk-doc gobject-introspection)
provides=(mingw-w64-pango=$pkgver)
conflicts=(mingw-w64-pango)
options=(!strip staticlibs !buildflags)
source=("http://ftp.gnome.org/pub/GNOME/sources/pango/${pkgver%.*}/pango-$pkgver.tar.xz"
        "http://pkgs.fedoraproject.org/cgit/mingw-pango.git/plain/pango-enable-delay-load-of-freetype-and-fontconfig.patch?h=f18"
        "http://pkgs.fedoraproject.org/cgit/mingw-pango.git/plain/pango_enable_static_build.patch?h=f18"
        "pango.modules")
md5sums=('d9532826e95bdb374355deebc42441bb'
         '757da453d21be895d4fbc31ffd45fb25'
         '9ead454899077d5a0cd354f32f924b4c'
         '967c8dec6087eac30dda63bac3bf58a0')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

build() {
  local tardir="$srcdir/${pkgname#fedora-mingw-w64-}-$pkgver"
  local builddir
  local arch

  # Apply patches from fedora repository
  cd "$tardir"
  patch -Np0 < '../pango-enable-delay-load-of-freetype-and-fontconfig.patch?h=f18'
  autoreconf -if
  patch -Np0 < '../pango_enable_static_build.patch?h=f18'

  unset LDFLAGS CPPFLAGS

  for arch in ${_architectures[@]}; do
    builddir="$srcdir/$pkgname-$pkgver-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    "$tardir/configure" \
      --build="$CHOST" \
      --prefix="/usr/$arch" \
      --host="$arch" \
      --enable-static \
      --enable-shared \
      --with-included-modules=basic-win32 \
      --enable-delay-load
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/$pkgname-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    # Native pango uses a %post script to generate this, but the
    # pango-querymodules.exe binary is not something we can easily run on
    # a Linux host. We could use wine but wine isn't happy in a mock
    # environment. So we just include a pre-generated copy on basis that
    # it won't ever change much.
    #
    # If you want to rebuild this, do a "wine pango-querymodules.exe > pango.modules"
    # and adjust paths accordingly.
    mkdir -p "$prefix/etc"
    cp -f "$srcdir/pango.modules" "$prefix/etc/"

    find "$prefix" -regex '.*\.\(exe\|bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip -xg {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +
    #rm -fr "$prefix/share"
  done
}

# vim:set ts=2 sw=2 et:
