# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-pango.git/

_realname=pango
pkgname="fedora-mingw-w64-${_realname}"
pkgver=1.36.8
pkgrel=5
pkgdesc='A library for layout and rendering of text (mingw-w64)'
arch=(any)
url='http://www.pango.org/'
license=(LGPL)
groups=(fedora-mingw)
depends=(mingw-w64-crt fedora-mingw-w64-cairo fedora-mingw-w64-expat
         fedora-mingw-w64-fontconfig fedora-mingw-w64-freetype
         fedora-mingw-w64-gettext fedora-mingw-w64-glib2
         fedora-mingw-w64-win-iconv mingw-w64-libpng
         fedora-mingw-w64-pixman fedora-mingw-w64-harfbuzz)
makedepends=(afm-mingw-w64-makedepends afm-mingw-w64-gobject-introspection gtk-doc wine)
provides=("mingw-w64-${_realname}=$pkgver")
conflicts=("mingw-w64-${_realname}")
options=(!strip staticlibs !buildflags)
source=("http://ftp.gnome.org/pub/GNOME/sources/${_realname}/${pkgver%.*}/${_realname}-$pkgver.tar.xz"
        "http://pkgs.fedoraproject.org/cgit/mingw-pango.git/plain/pango-enable-delay-load-of-freetype-and-fontconfig.patch?h=f18"
        "0002-msvc-is-impotent-but-not.mingw.patch"
        "pango.modules")
md5sums=('217a9a753006275215fa9fa127760ece'
         '757da453d21be895d4fbc31ffd45fb25'
         'd1bee1980c28ea7be068f030a8c6759e'
         '967c8dec6087eac30dda63bac3bf58a0')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

prepare() {
  cd "$srcdir/${_realname}-$pkgver"

  patch -Np0 < '../pango-enable-delay-load-of-freetype-and-fontconfig.patch?h=f18'

  # Avoid build failure because a couple of functions are not exported:
  # https://github.com/Alexpux/MINGW-packages/blob/master/mingw-w64-pango/0002-msvc-is-impotent-but-not.mingw.patch
  patch -Np1 < ../0002-msvc-is-impotent-but-not.mingw.patch

  gtkdocize
  autoreconf -ivf
}

build() {
  local tardir="$srcdir/${_realname}-$pkgver"
  local builddir
  local arch

  unset LDFLAGS CPPFLAGS

  for arch in ${_architectures[@]}; do
    builddir="$tardir-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    # With --enable-delay-load the build fails while generating
    # PangoCairo.gir with hieroglyphical errors
    source "$arch-configure" "$tardir" \
      --enable-static \
      --enable-shared \
      --disable-silent-rules \
      --enable-introspection \
      --with-included-modules=basic-win32
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/${_realname}-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    # Native pango uses a %post script to generate this, but the
    # pango-querymodules.exe binary is not something we can easily run on
    # a Linux host. We could use wine but wine isn't happy in a mock
    # environment. So we just include a pre-generated copy on basis that
    # it won't ever change much.
    #
    # If you want to rebuild this, do a "wine pango-querymodules.exe > pango.modules"
    # and adjust paths accordingly.
    mkdir -p "$prefix/etc"
    cp -f "$srcdir/pango.modules" "$prefix/etc/"

    find "$prefix" -regex '.*\.\(bat\|def\|exe\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip -xg {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +

    rm -fr "$prefix/share/man"
  done
}

# vim:set ts=2 sw=2 et:
