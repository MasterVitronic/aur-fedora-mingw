# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-pango.git/

pkgname=$CARCH-mingw-w64-pango
pkgver=1.40.7
pkgrel=2
pkgdesc='A library for layout and rendering of text (mingw-w64)'
arch=(any)
url='http://www.pango.org/'
license=(LGPL)
groups=(afm-$CARCH)
depends=(
    mingw-w64-crt
    $CARCH-mingw-w64-cairo
    $CARCH-mingw-w64-expat
    $CARCH-mingw-w64-fontconfig
    $CARCH-mingw-w64-freetype
    $CARCH-mingw-w64-gettext
    $CARCH-mingw-w64-glib2
    $CARCH-mingw-w64-win-iconv
    $CARCH-mingw-w64-libpng
    $CARCH-mingw-w64-pixman
    $CARCH-mingw-w64-harfbuzz
)
makedepends=(
    afm-mingw-w64-makedepends
    $CARCH-mingw-w64-gobject-introspection
    gtk-doc
    wine
)
source=("http://ftp.gnome.org/pub/GNOME/sources/pango/${pkgver%.*}/pango-$pkgver.tar.xz"
        "http://pkgs.fedoraproject.org/cgit/rpms/mingw-pango.git/plain/pango-enable-delay-load-of-freetype-and-fontconfig.patch?id=d431b89bb797e3ae35231dc31641f31f16f7d1bf")
md5sums=('46a9e38a502365a7b12d3dc8a5ac5c06'
         '8c11dd896e36885708dce1aacde48cbf')

prepare() {
  cd "$srcdir/pango-$pkgver"

  patch -Np1 < '../pango-enable-delay-load-of-freetype-and-fontconfig.patch?id=d431b89bb797e3ae35231dc31641f31f16f7d1bf'

  gtkdocize
  autoreconf -ivf
}

build() {
  local tardir="$srcdir/pango-$pkgver"
  local builddir="$tardir-build"

  mkdir -p "$builddir"
  cd "$builddir"
  # With --enable-delay-load the build fails while generating
  # PangoCairo.gir with hieroglyphical errors
  source $CHOST-configure "$tardir" \
    --enable-static \
    --enable-shared \
    --disable-silent-rules \
    --enable-introspection
  make
}

package() {
  local prefix="$pkgdir/usr/$CHOST"

  cd "$srcdir/pango-$pkgver-build"
  make DESTDIR="$pkgdir" install

  find "$prefix" -name '*.exe' -exec $CHOST-strip {} +
  find "$prefix" -name '*.dll' -exec $CHOST-strip --strip-unneeded {} +
  find "$prefix" -name '*.a'   -exec $CHOST-strip -g {} +
}

# vim:set ts=2 sw=2 et:
