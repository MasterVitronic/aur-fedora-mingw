# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-pango.git/

_realname=pango
pkgname="fedora-mingw-w64-${_realname}"
pkgver=1.40.6
pkgrel=1
pkgdesc='A library for layout and rendering of text (mingw-w64)'
arch=(any)
url='http://www.pango.org/'
license=(LGPL)
groups=(aur-fedora-mingw fedora-mingw)
depends=(mingw-w64-crt fedora-mingw-w64-cairo fedora-mingw-w64-expat
         fedora-mingw-w64-fontconfig fedora-mingw-w64-freetype
         fedora-mingw-w64-gettext fedora-mingw-w64-glib2
         fedora-mingw-w64-win-iconv mingw-w64-libpng
         fedora-mingw-w64-pixman fedora-mingw-w64-harfbuzz)
makedepends=(afm-mingw-w64-makedepends mingw-w64-gobject-introspection gtk-doc wine)
provides=("mingw-w64-${_realname}=$pkgver")
conflicts=("mingw-w64-${_realname}")
options=(!strip staticlibs !buildflags)
source=("http://ftp.gnome.org/pub/GNOME/sources/${_realname}/${pkgver%.*}/${_realname}-$pkgver.tar.xz"
        "http://pkgs.fedoraproject.org/cgit/rpms/mingw-pango.git/plain/pango-enable-delay-load-of-freetype-and-fontconfig.patch?id=d431b89bb797e3ae35231dc31641f31f16f7d1bf")
md5sums=('507c6746fbf53fc9d48c577f1e265de3'
         '8c11dd896e36885708dce1aacde48cbf')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

prepare() {
  cd "$srcdir/${_realname}-$pkgver"

  patch -Np1 < '../pango-enable-delay-load-of-freetype-and-fontconfig.patch?id=d431b89bb797e3ae35231dc31641f31f16f7d1bf'

  gtkdocize
  autoreconf -ivf
}

build() {
  local tardir="$srcdir/${_realname}-$pkgver"
  local builddir
  local arch

  for arch in ${_architectures[@]}; do
    builddir="$tardir-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    # With --enable-delay-load the build fails while generating
    # PangoCairo.gir with hieroglyphical errors
    source $arch-configure "$tardir" \
      --enable-static \
      --enable-shared \
      --disable-silent-rules \
      --enable-introspection
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/${_realname}-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    find "$prefix" -regex '.*\.\(bat\|def\|exe\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip --strip-unneeded {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +

    rm -fr "$prefix/share/man"
  done
}

# vim:set ts=2 sw=2 et:
