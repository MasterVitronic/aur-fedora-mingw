# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-nsis.git/

pkgname=afm-mingw-w64-nsis
pkgver=3.01
pkgrel=3
pkgdesc='A professional open source system to create Windows installers'
arch=(i686 x86_64)
url='http://nsis.sourceforge.net/'
license=(custom:zlib)
groups=(afm-i686 afm-x86_64)
conflicts=(mingw-w64-nsis)
provides=(mingw-w64-nsis)
optdepends=($arch-mingw-w64-zlib)
makedepends=(
    afm-mingw-w64-makedepends
    $arch-mingw-w64-zlib
    scons
)
source=("http://sourceforge.net/projects/nsis/files/NSIS 3/$pkgver/nsis-$pkgver-src.tar.bz2")
md5sums=('fc40b977d1ddd9f8a74511147f38ca8c')

# See RHBZ#1090075
_sconsopts=(VERSION="$pkgver" PREFIX=/usr PREFIX_CONF=/etc ZLIB_W32=/usr/i686-w64-mingw32 SKIPUTILS='NSIS Menu' STRIP_CP=false NSIS_MAX_STRLEN=8192)

build() {
  cd "$srcdir/nsis-$pkgver-src"
  scons "${_sconsopts[@]}"
}

package() {
  cd "$srcdir/nsis-$pkgver-src"
  scons PREFIX_DEST="$pkgdir" "${_sconsopts[@]}" install
}

# vim:set ts=2 sw=2 et:
