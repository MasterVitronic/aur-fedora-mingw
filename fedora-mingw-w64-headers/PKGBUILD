# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-headers.git/

pkgname=fedora-mingw-w64-headers
pkgver=6155
pkgrel=1
pkgdesc='MinGW-w64 headers for Windows'
arch=(any)
url='http://mingw-w64.sourceforge.net'
license=('custom')
groups=('fedora-mingw')
depends=()
makedepends=(svn)
optdepends=()
provides=(mingw-w64-headers)
conflicts=()
replaces=(mingw-w64-headers)
backup=()
options=(!strip !buildflags !libtool)
source=()
md5sums=()

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)
_svnmod='mingw-w64'
_svntrunk='svn://svn.code.sf.net/p/mingw-w64/code/trunk'

build() {
  local svndir="$srcdir/$_svnmod"
  local builddir
  local arch

  if [[ -d "$svndir/.svn" ]]; then
    cd "$_svnmod"
    svn up -r "$pkgver"
    svn revert -R .
  else
    cd "$srcdir"
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod" && cd "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"

  export CFLAGS='-O2 -pipe'
  export CXXFLAGS="$CFLAGS"
  unset  LDFLAGS
  unset  CPPFLAGS

  for arch in ${_architectures[@]}; do
    builddir="$srcdir/headers-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    "$svndir/mingw-w64-headers/configure" \
      --prefix="/usr/$arch" \
      --host="$arch" \
      --enable-sdk=all \
      --enable-secure-api
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/headers-$arch"
    make DESTDIR="$pkgdir" install
  done
}

# vim:set ts=2 sw=2 et:
