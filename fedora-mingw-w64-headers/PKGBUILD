# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-headers.git/

pkgname=fedora-mingw-w64-headers
pkgver=3.0.0
pkgrel=1
pkgdesc='MinGW-w64 headers for Windows'
arch=(any)
url='http://mingw-w64.sourceforge.net/'
license=('custom')
groups=('fedora-mingw')
depends=('mingw-w64-winpthreads')
makedepends=()
optdepends=()
provides=(mingw-w64-headers)
conflicts=()
replaces=()
backup=()
options=(!strip !buildflags !libtool)
source=("http://downloads.sourceforge.net/sourceforge/mingw-w64/mingw-w64/mingw-w64-v$pkgver.tar.bz2")
md5sums=('22b0030702fe00927daf1ae6ddfb526e')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

build() {
  local tardir="$srcdir/mingw-w64-v$pkgver"
  local builddir
  local arch

  export CFLAGS='-O2 -pipe'
  export CXXFLAGS="$CFLAGS"
  unset  LDFLAGS
  unset  CPPFLAGS

  for arch in ${_architectures[@]}; do
    builddir="$srcdir/$pkgname-$pkgver-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    "$tardir/mingw-w64-headers/configure" \
      --prefix="/usr/$arch" \
      --host="$arch" \
      --enable-sdk=all \
      --enable-secure-api
  done
}

package() {
  local arch
  local prefix
  local includedir

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"
    includedir="$prefix/include"

    cd "$srcdir/$pkgname-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    rm -f "$includedir/pthread_signal.h" \
          "$includedir/pthread_time.h"   \
          "$includedir/pthread_unistd.h" \
          "$includedir/pthread_signal.h" \
          "$includedir/pthread_time.h"   \
          "$includedir/pthread_unistd.h"
  done
}

# vim:set ts=2 sw=2 et:
