# Maintainer: Nicola Fontana <ntd@entidi.it>

_realname=gobject-introspection
pkgname="afm-mingw-w64-${_realname}"
pkgver=1.52.1
pkgrel=1
pkgdesc='Introspection system for GObject-based libraries (mingw-w64)'
arch=(any)
url='https://wiki.gnome.org/Projects/GObjectIntrospection'
license=(LGPL GPL)
groups=(aur-fedora-mingw)
depends=(
    mingw-w64-crt
    "fedora-mingw-w64-glib2>=2.52.1"
    python2
)
makedepends=(
    afm-mingw-w64-makedepends
    "glib2>=2.52.1"     # Host glib2 should match build glib2 version
    wine
)
provides=("mingw-w64-${_realname}")
conflicts=("mingw-w64-${_realname}")
source=("http://ftp.gnome.org/pub/GNOME/sources/${_realname}/${pkgver%.*}/${_realname}-$pkgver.tar.xz"
        "0003-Allow-overriding-of-the-host-os-name.patch"
        "0007-Allow-to-skip-the-check-for-Python-headers.patch"
        "0008-Overcome-File-name-too-long-error.patch"
        "0009-Customize-CCompiler-on-MinGW-cross-compilation.patch"
        "winewrapper")
md5sums=('34157073991f9eeb0ed953351b65eb61'
         '3183cc3a8a14338f27830f0ebb66d408'
         '1420edbbec79268a490b0f781a4dacd9'
         '9fc71145c77b0688c0c28616fbef8d7a'
         '7b4be4221fd52104bc69bee42255742c'
         '83cf11e501161eae90df56838c75653f')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

prepare() {
  cd "$srcdir/${_realname}-$pkgver"

  # https://bugzilla.gnome.org/show_bug.cgi?id=761985
  patch -Np1 < '../0003-Allow-overriding-of-the-host-os-name.patch'

  patch -Np1 < '../0007-Allow-to-skip-the-check-for-Python-headers.patch'
  patch -Np1 < '../0008-Overcome-File-name-too-long-error.patch'
  patch -Np1 < '../0009-Customize-CCompiler-on-MinGW-cross-compilation.patch'

  NOCONFIGURE=1 ./autogen.sh

  # Clear environment trees created by wine
  rm -fr "$srcdir/win32" "$srcdir/win64"
}

build() {
  local tardir="$srcdir/${_realname}-$pkgver"
  local builddir
  local arch

  # Force the use of the python-2 interpreter
  export PYTHON=/usr/bin/python2

  for arch in ${_architectures[@]}; do
    builddir="$tardir-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"

    # Build the python module with build == host
    (
      # Do it in a subshell so the environment can be set accordingly
      # and it will not be leaked outside
      source /etc/makepkg.conf
      "$tardir/configure" \
        --disable-silent-rules \
        --enable-shared \
        --enable-static
      make scannerparser.c _giscanner.la
    )

    # Cross-compile the rest of the package
    (
      # Again, do it inside a subshell to avoid environment leakings
      source $arch-configure "$tardir" \
        --host="$arch" \
        --disable-silent-rules \
        --enable-shared \
        --enable-static \
        --disable-doctool \
        --disable-giscanner

      # Avoid overriding what built previously
      make -t scannerparser.c _giscanner.la

      make
    )
  done
}

package() {
  local arch
  local prefix
  local bits

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    case "$arch" in
      *i686*)
        bits=32
        ;;
      *x86_64*)
        bits=64
        ;;
      *)
        return 1
        ;;
    esac

    cd "$srcdir/${_realname}-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    # Wrappers for running g-ir-compiler and g-ir-generate with wine
    sed \
      -e "s|@arch@|win$bits|g" \
      -e "s|@exe@|/usr/$arch/bin/g-ir-compiler.exe|g" \
      "$srcdir/winewrapper" > "$prefix/bin/g-ir-compiler"
    chmod 0755 "$prefix/bin/g-ir-compiler"
    sed \
      -e "s|@arch@|win$bits|g" \
      -e "s|@exe@|/usr/$arch/bin/g-ir-compiler.exe|g" \
      "$srcdir/winewrapper" > "$prefix/bin/g-ir-generate"
    chmod 0755 "$prefix/bin/g-ir-generate"

    # Substitute the wrappers to the exe in the pkg-config files
    sed -i \
      -e 's|g-ir-compiler.exe|g-ir-compiler|g' \
      -e 's|g-ir-generate.exe|g-ir-generate|g' \
      $prefix/lib/pkgconfig/gobject-introspection-1.0.pc

    find "$prefix" -regex '.*\.\(bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip --strip-unneeded {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +
    rm -fr "$prefix/share/man"
    rm -fr "$prefix/share/aclocal"
  done
}

# vim:set ts=2 sw=2 et:
