# Maintainer: Nicola Fontana <ntd@entidi.it>

_realname=gobject-introspection
pkgname="afm-mingw-w64-${_realname}-git"
pkgver=1.46.0.r113.cd355e8
pkgrel=5
pkgdesc='Introspection system for GObject-based libraries (mingw-w64)'
arch=(any)
url='https://live.gnome.org/GObjectIntrospection'
license=(LGPL GPL)
groups=(aur-fedora-mingw)
depends=(mingw-w64-crt fedora-mingw-w64-glib2 python2)
makedepends=(git afm-mingw-w64-makedepends wine)
provides=("mingw-w64-${_realname}")
conflicts=("mingw-w64-${_realname}")
options=(!strip staticlibs !buildflags)
source=("git+git://git.gnome.org/gobject-introspection"
        "0001-Use-PKG_CONFIG-when-running-g-ir-scanner.patch"
        "0002-Allow-overriding-of-dlltool-name.patch"
        "0003-Allow-overriding-of-the-host-os-name.patch"
        "0005-Assume-gcc-use-the-same-path-separator-as-python.patch"
        "0006-Use-a-launcher-for-executing-g-ir-scanner.patch"
        "0007-Allow-to-skip-the-check-for-Python-headers.patch"
        "0008-Overcome-File-name-too-long-error.patch"
        "0009-Customize-CCompiler-on-MinGW-cross-compilation.patch"
        "0001-Remove-leading-SHELL-in-libtool-call.patch"
        "winewrapper")
md5sums=('SKIP'
         '3484c2c13d837a15262556a29982d51d'
         'dfa5b106891a5b15655615f892b8995a'
         '3183cc3a8a14338f27830f0ebb66d408'
         'a82fe820433cfe43972b13a02a1928c0'
         '4270a18526585ef48080241f5718ddbb'
         '1420edbbec79268a490b0f781a4dacd9'
         '9fc71145c77b0688c0c28616fbef8d7a'
         '7b4be4221fd52104bc69bee42255742c'
         '039267df90b681240023bf1d4d775c72'
         'ee07cea6a2f3b351ea5f873c8a569fd6')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

pkgver() {
  cd "$srcdir/${_realname}"

  printf "%s" "$(git describe --long | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

prepare() {
  cd "$srcdir/${_realname}"

  # https://bugzilla.gnome.org/show_bug.cgi?id=761983
  patch -Np1 < '../0001-Use-PKG_CONFIG-when-running-g-ir-scanner.patch'

  # https://bugzilla.gnome.org/show_bug.cgi?id=761984
  patch -Np1 < '../0002-Allow-overriding-of-dlltool-name.patch'

  # https://bugzilla.gnome.org/show_bug.cgi?id=761985
  patch -Np1 < '../0003-Allow-overriding-of-the-host-os-name.patch'

  # https://bugzilla.gnome.org/show_bug.cgi?id=761981
  patch -Np1 < '../0005-Assume-gcc-use-the-same-path-separator-as-python.patch'

  # https://bugzilla.gnome.org/show_bug.cgi?id=696773
  patch -Np1 < '../0006-Use-a-launcher-for-executing-g-ir-scanner.patch'

  patch -Np1 < '../0007-Allow-to-skip-the-check-for-Python-headers.patch'
  patch -Np1 < '../0008-Overcome-File-name-too-long-error.patch'
  patch -Np1 < '../0009-Customize-CCompiler-on-MinGW-cross-compilation.patch'

  # https://bugzilla.gnome.org/show_bug.cgi?id=761982
  patch -Np1 < '../0001-Remove-leading-SHELL-in-libtool-call.patch'

  NOCONFIGURE=1 ./autogen.sh

  # Clear environment trees created by wine
  rm -fr "$srcdir/win32" "$srcdir/win64"
}

build() {
  local tardir="$srcdir/${_realname}"
  local builddir
  local arch
  local bits

  # Force the use of the python-2 interpreter
  export PYTHON=/usr/bin/python2

  for arch in ${_architectures[@]}; do
    builddir="$tardir-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"

    # Build the python module with build == host
    "$tardir/configure" \
      --disable-silent-rules \
      --enable-shared \
      --enable-static
    make scannerparser.c _giscanner.la

    # Cross-compile the rest of the package inside a subshell so
    # there will not be environment variable leaks from $arch-configure.
    # The $arch-configure script comes from afm-mingw-w64-makedepends.
    (
      source $arch-configure "$tardir" \
        --host="$arch" \
        --disable-silent-rules \
        --enable-shared \
        --enable-static \
        --disable-doctool \
        --disable-giscanner

      # Avoid overriding what built with build == host
      make -t scannerparser.c _giscanner.la

      make INTROSPECTION_LAUNCHER="$INTROSPECTION_LAUNCHER"
    )
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/${_realname}-build-$arch"
    make DESTDIR="$pkgdir" install

    # Wrappers for running g-ir-compiler and g-ir-generate with wine
    sed \
      -e "s|@arch@|win$bits|g" \
      -e "s|@exe@|/usr/$arch/bin/g-ir-compiler.exe|g" \
      "$srcdir/winewrapper" > "$prefix/bin/g-ir-compiler"
    chmod 0755 "$prefix/bin/g-ir-compiler"
    sed \
      -e "s|@arch@|win$bits|g" \
      -e "s|@exe@|/usr/$arch/bin/g-ir-compiler.exe|g" \
      "$srcdir/winewrapper" > "$prefix/bin/g-ir-generate"
    chmod 0755 "$prefix/bin/g-ir-generate"

    # Substitute the wrappers to the exe in the pkg-config files
    sed -i \
      -e 's|g-ir-compiler.exe|g-ir-compiler|g' \
      -e 's|g-ir-generate.exe|g-ir-generate|g' \
      $prefix/lib/pkgconfig/gobject-introspection-1.0.pc

    find "$prefix" -regex '.*\.\(bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip --strip-unneeded {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +
    rm -fr "$prefix/share/man"
    rm -fr "$prefix/share/aclocal"
  done
}

# vim:set ts=2 sw=2 et:
