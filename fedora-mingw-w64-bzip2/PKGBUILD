# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-bzip2.git/

_realname=bzip2
pkgname="fedora-mingw-w64-${_realname}"
pkgver=1.0.6
pkgrel=12
pkgdesc='A high-quality data compression program (mingw-w64)'
arch=(any)
url='http://www.bzip.org/'
license=(custom)
groups=(fedora-mingw)
depends=(mingw-w64-crt)
makedepends=(afm-mingw-w64-makedepends)
provides=("mingw-w64-${_realname}=$pkgver")
conflicts=("mingw-w64-${_realname}")
options=(!strip staticlibs !buildflags)
source=("http://www.bzip.org/$pkgver/${_realname}-$pkgver.tar.gz"
        "http://pkgs.fedoraproject.org/cgit/mingw-bzip2.git/plain/bzip2-1.0.4-bzip2recover.patch?h=f18"
        "http://pkgs.fedoraproject.org/cgit/mingw-bzip2.git/plain/mingw32-bzip2-1.0.5-slash.patch?h=f18"
        "http://pkgs.fedoraproject.org/cgit/mingw-bzip2.git/plain/bzip2-1.0.5-autoconfiscated.patch?h=f18"
        "http://pkgs.fedoraproject.org/cgit/mingw-bzip2.git/plain/bzip2-use-cdecl-calling-convention.patch?h=f18")
md5sums=('00b516f4704d4a7cb50a1d97e6e8e15b'
         '8a8bca02bdeaf2fd8913aeb549577e7e'
         '474f7aa4ec7b09f2f0e13358dadc5679'
         '1abfff3b2a2b035dc03a15e90caa947c'
         '5562a217855e9fe2f1ce489feff15290')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

prepare() {
  cd "$srcdir/${_realname}-$pkgver"

  patch -Np1 < '../bzip2-1.0.4-bzip2recover.patch?h=f18'
  patch -Np1 < '../mingw32-bzip2-1.0.5-slash.patch?h=f18'
  patch -Np1 < '../bzip2-1.0.5-autoconfiscated.patch?h=f18'
  patch -Np1 < '../bzip2-use-cdecl-calling-convention.patch?h=f18'

  sh ./autogen.sh
}

build() {
  local tardir="$srcdir/${_realname}-$pkgver"
  local builddir
  local arch

  unset LDFLAGS CPPFLAGS

  for arch in ${_architectures[@]}; do
    builddir="$tardir-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    source "$arch-configure" "$tardir"
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/${_realname}-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    find "$prefix" -regex '.*\.\(exe\|bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip -xg {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +
    rm -fr "$prefix/share"
    rm -f  "$prefix/bin/bzdiff"
    rm -f  "$prefix/bin/bzgrep"
    rm -f  "$prefix/bin/bzmore"
  done
}

# vim:set ts=2 sw=2 et:
