# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-harfbuzz.git/

pkgname=$CARCH-mingw-w64-harfbuzz
pkgver=1.4.4
pkgrel=3
pkgdesc='OpenType text shaping engine (mingw-w64)'
arch=(any)
url='http://www.harfbuzz.org/'
license=(MIT)
groups=(afm-$CARCH)
depends=(
    mingw-w64-crt
    $CARCH-mingw-w64-glib2
    $CARCH-mingw-w64-freetype
    $CARCH-mingw-w64-cairo
    $CARCH-mingw-w64-icu
)
makedepends=(
    afm-mingw-w64-makedepends
    $CARCH-mingw-w64-gobject-introspection
    wine
)
source=("http://www.freedesktop.org/software/harfbuzz/release/harfbuzz-$pkgver.tar.bz2"
        "http://pkgs.fedoraproject.org/cgit/rpms/mingw-harfbuzz.git/plain/harfbuzz-enable-delayload-freetype.patch?id=ffe49919d88f9980d197c48066eefd67d3ac072e")
md5sums=('ddac65a48b1d2c6dda8bddf1f37faeb3'
         'fcd307058298447861570ad68415d7e0')

prepare() {
  cd "$srcdir/harfbuzz-$pkgver"

  # Make freetype an optional runtime dependency instead of a hard dependency (using delay load)
  patch -Np1 < '../harfbuzz-enable-delayload-freetype.patch?id=ffe49919d88f9980d197c48066eefd67d3ac072e'

  autoreconf -ivf
}

build() {
  local tardir="$srcdir/harfbuzz-$pkgver"
  local builddir="$tardir-build"

  mkdir -p "$builddir"
  cd "$builddir"
  source $CHOST-configure "$tardir" \
    --enable-static \
    --enable-shared \
    --disable-silent-rules \
    --with-gobject \
    --enable-introspection
  make
}

package() {
  local prefix="$pkgdir/usr/$CHOST"

  cd "$srcdir/harfbuzz-$pkgver-build"
  make DESTDIR="$pkgdir" install

  find "$prefix" -name '*.exe' -exec $CHOST-strip {} +
  find "$prefix" -name '*.dll' -exec $CHOST-strip --strip-unneeded {} +
  find "$prefix" -name '*.a'   -exec $CHOST-strip -g {} +
}

# vim:set ts=2 sw=2 et:
