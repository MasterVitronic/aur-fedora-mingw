# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# This package automatically pulls in the makedepends needed for MinGW
# cross-building and provides the following helper stuff:
#
# - pkg-config wrappers
# - CMake configurations for MinGW
# - configure wrappers
#
# It borrows ideas from the mingw-filesystem Fedora package [1] and some
# AUR packages [2] [3] that in my opinion has wrong names.
#
# [1] http://pkgs.fedoraproject.org/cgit/mingw-filesystem.git/
# [2] https://aur.archlinux.org/packages/mingw-w64-pkg-config/
# [3] https://aur.archlinux.org/packages/mingw-w64-cmake/

pkgname=afm-mingw-w64-makedepends
pkgver=3
pkgrel=8
pkgdesc='A collection of stuff needed for MinGW cross-building'
arch=(any)
url='http://dev.entidi.com/p/aur-fedora-mingw/'
license=(mit)
groups=(afm-i686 afm-x86_64)
depends=(mingw-w64-gcc)
conflicts=(mingw-w64-pkg-config mingw-w64-cmake)
provides=(mingw-w64-pkg-config mingw-w64-cmake)
source=("configure-wrapper"
        "i686-w64-mingw32-cmake"
        "i686-w64-mingw32-configure"
        "i686-w64-mingw32-pkg-config"
        "makepkg.common.conf"
        "makepkg.i686.conf"
        "makepkg.native.conf"
        "makepkg.x86_64.conf"
        "x86_64-w64-mingw32-cmake"
        "x86_64-w64-mingw32-configure"
        "x86_64-w64-mingw32-pkg-config"
        "Toolchain-i686-w64-mingw32.cmake"
        "Toolchain-x86_64-w64-mingw32.cmake")
md5sums=('872de7f30558017886e0bc62f1fa3f77'
         '4abb097f8387f364a9877e28c7c880a3'
         '2fe711bc73b89cff50baa5b785e35b4c'
         '2c2641547debc5d289bb85b0939e4380'
         '9bf6ea428f78d5f163c4b23d48eba9e1'
         'a38a29dd8f512e71a657490c9c82004c'
         'f8123a57a0d15de814c23668334e3dca'
         '80969e92a600cec8de480b64bcefaa98'
         '23961cda7ebd6a35aeff3345c780c6b5'
         'e08642fb0279dd280b85ad1fb1b6215e'
         '0a879bf20cd82e44b73eb49cf2484eda'
         '4704995815374af7b3b853b139d04ad5'
         'cdb9f76c5ab95038df1c03755e9119e7')

package() {
  local bindir="$pkgdir/usr/bin"
  local sharedir="$pkgdir/usr/share/mingw"

  install -d "$bindir"
  install -d "$sharedir"

  # Find the root directory and substitute it in makepkg.common.conf
  local root=$(cd "$startdir/../.." && echo $PWD)
  sed \
    -e "s|@root@|$root|g" \
    "$srcdir/makepkg.common.conf" > "$sharedir/makepkg.common.conf"
  chmod 0644 "$sharedir/makepkg.common.conf"

  install -m 755 i686-w64-mingw32-cmake             "$bindir/"
  install -m 755 x86_64-w64-mingw32-cmake           "$bindir/"
  install -m 755 i686-w64-mingw32-configure         "$bindir/"
  install -m 755 x86_64-w64-mingw32-configure       "$bindir/"
  install -m 755 i686-w64-mingw32-pkg-config        "$bindir/"
  install -m 755 x86_64-w64-mingw32-pkg-config      "$bindir/"

  install -m 644 configure-wrapper                  "$sharedir/"
  install -m 644 makepkg.i686.conf                  "$sharedir/"
  install -m 644 makepkg.native.conf                "$sharedir/"
  install -m 644 makepkg.x86_64.conf                "$sharedir/"
  install -m 644 Toolchain-i686-w64-mingw32.cmake   "$sharedir/"
  install -m 644 Toolchain-x86_64-w64-mingw32.cmake "$sharedir/"
}

# vim:set ts=2 sw=2 et:
