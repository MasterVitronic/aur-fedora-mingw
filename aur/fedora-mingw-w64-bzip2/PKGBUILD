# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-bzip2.git/

pkgname=$CARCH-mingw-w64-bzip2
pkgver=1.0.6
pkgrel=19
pkgdesc='A high-quality data compression program (mingw-w64)'
arch=(any)
url='http://www.bzip.org/'
license=(custom)
groups=(afm-$CARCH)
depends=(mingw-w64-crt)
makedepends=(afm-mingw-w64-makedepends)
source=("http://www.bzip.org/$pkgver/bzip2-$pkgver.tar.gz"
        "http://pkgs.fedoraproject.org/cgit/mingw-bzip2.git/plain/bzip2-1.0.4-bzip2recover.patch?h=f18"
        "http://pkgs.fedoraproject.org/cgit/mingw-bzip2.git/plain/mingw32-bzip2-1.0.5-slash.patch?h=f18"
        "http://pkgs.fedoraproject.org/cgit/mingw-bzip2.git/plain/bzip2-1.0.5-autoconfiscated.patch?h=f18"
        "http://pkgs.fedoraproject.org/cgit/mingw-bzip2.git/plain/bzip2-use-cdecl-calling-convention.patch?h=f18")
md5sums=('00b516f4704d4a7cb50a1d97e6e8e15b'
         '8a8bca02bdeaf2fd8913aeb549577e7e'
         '474f7aa4ec7b09f2f0e13358dadc5679'
         '1abfff3b2a2b035dc03a15e90caa947c'
         '5562a217855e9fe2f1ce489feff15290')

prepare() {
  cd "$srcdir/bzip2-$pkgver"

  patch -Np1 < '../bzip2-1.0.4-bzip2recover.patch?h=f18'

  patch -Np1 < '../mingw32-bzip2-1.0.5-slash.patch?h=f18'

  patch -Np1 < '../bzip2-1.0.5-autoconfiscated.patch?h=f18'

  patch -Np1 < '../bzip2-use-cdecl-calling-convention.patch?h=f18'

  sh ./autogen.sh
}

build() {
  local tardir="$srcdir/bzip2-$pkgver"
  local builddir="$tardir-build"

  mkdir -p "$builddir"
  cd "$builddir"
  source $CHOST-configure "$tardir"
  make
}

package() {
  local prefix="$pkgdir/usr/$CHOST"

  cd "$srcdir/bzip2-$pkgver-build"
  make DESTDIR="$pkgdir" install

  find "$prefix" -name '*.dll' -exec $CHOST-strip --strip-unneeded {} +
  find "$prefix" -name '*.a'   -exec $CHOST-strip -g {} +

  rm -fr "$prefix/share"
  rm -f  "$prefix/bin/bzdiff"
  rm -f  "$prefix/bin/bzgrep"
  rm -f  "$prefix/bin/bzmore"
}

# vim:set ts=2 sw=2 et:
