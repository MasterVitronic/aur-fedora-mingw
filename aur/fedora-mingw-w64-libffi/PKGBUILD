# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-libffi.git/

pkgname=$CARCH-mingw-w64-libffi
pkgver=3.1
pkgrel=3
pkgdesc='A portable, high level programming interface to various calling conventions (mingw-w64)'
arch=(any)
url='http://sourceware.org/libffi/'
license=(MIT)
groups=(afm-$CARCH)
depends=(mingw-w64-crt)
makedepends=(mingw-w64-gcc)
source=("ftp://sourceware.org/pub/libffi/libffi-$pkgver.tar.gz")
md5sums=('f5898b29bbfd70502831a212d9249d10')

build() {
  local tardir="$srcdir/libffi-$pkgver"
  local builddir="$tardir-build"

  mkdir -p "$builddir"
  cd "$builddir"
  source $CHOST-configure "$tardir" \
    --enable-shared
  make
}

package() {
  local prefix="$pkgdir/usr/$CHOST"

  cd "$srcdir/libffi-$pkgver-build"
  make DESTDIR="$pkgdir" install

  find "$prefix" -name '*.exe' -exec $CHOST-strip {} +
  find "$prefix" -name '*.dll' -exec $CHOST-strip --strip-unneeded {} +
  find "$prefix" -name '*.a'   -exec $CHOST-strip -g {} +
}

# vim:set ts=2 sw=2 et:
