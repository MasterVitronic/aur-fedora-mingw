# Maintainer: Nicola Fontana <ntd@entidi.it>
# Maintainer: Alexey Pavlov <alexpux@gmail.com>
#
# This is an adaptation of the MinGW package by Alexpux found on GitHub:
# https://github.com/Alexpux/MINGW-packages/tree/master/mingw-w64-lua

pkgname=$CARCH-mingw-w64-lua
pkgver=5.3.4
pkgrel=2
pkgdesc='Powerful light-weight programming language designed for extending applications (mingw-w64)'
arch=(any)
url='http://www.lua.org/'
license=(MIT)
groups=(afm-$CARCH)
depends=(mingw-w64-crt)
makedepends=(mingw-w64-gcc)
source=("http://www.lua.org/ftp/lua-$pkgver.tar.gz"
        "lua.pc"
        "implib.patch"
        "searchpath.patch"
        "strip.patch")
md5sums=('53a9c68bcc0eda58bdc2095ad5cdfc63'
         'e7ba6c2b695b0b84a5ea0cbff5fc9067'
         '311bd695726bd42279e7d786a494b9cf'
         '4fdfcdefca552092504a18ff14100c29'
         '18d00b876e3aa0d10d2595048b153be7')

prepare() {
  cd "$srcdir/lua-$pkgver"

  patch -Np0 < ../implib.patch

  patch -Np1 < ../searchpath.patch

  # Allow to use architecture dependent strip
  patch -Np1 < ../strip.patch
}

build() {
  local tardir="$srcdir/lua-$pkgver"
  local builddir="$tardir-build"

  # Lua does not support VPATH builds
  cp -fr "$tardir" "$builddir"
  sed -e "s|%VER%|${pkgver%.*}|g" \
      -e "s|%REL%|$pkgver|g" \
      -e "s|/usr|/usr/$CHOST|g" \
      "$srcdir/lua.pc" > "$builddir/lua.pc"

  cd "$builddir"
  make -j1 \
    MYCFLAGS="$CFLAGS" \
    CC=$CHOST-gcc \
    AR="$CHOST-ar rcu" \
    RANLIB=$CHOST-ranlib \
    STRIP=$CHOST-strip \
    mingw
}

package() {
  local builddir="$srcdir/lua-$pkgver-build"
  local prefix="$pkgdir/usr/$CHOST"

  cd "$builddir"
  make -j1 \
    INSTALL_TOP="$prefix" \
    TO_BIN="lua.exe luac.exe lua53.dll" \
    TO_LIB="liblua.a liblua.dll.a" \
    install

  # Manually install the pkg-config file
  install -Dm644 "$builddir/lua.pc" "$prefix/lib/pkgconfig/lua${pkgver%.*}.pc"
  # This symlink should be done only for the current Lua release
  ln -s "lua${pkgver%.*}.pc" "$prefix/lib/pkgconfig/lua.pc"

  find "$prefix" -name '*.exe' -exec $CHOST-strip {} +
  find "$prefix" -name '*.dll' -exec $CHOST-strip --strip-unneeded {} +
  find "$prefix" -name '*.a'   -exec $CHOST-strip -g {} +
}

# vim:set ts=2 sw=2 et:
