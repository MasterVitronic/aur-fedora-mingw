# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-libepoxy.git/

pkgname=$CARCH-mingw-w64-libepoxy
pkgver=1.4.1
pkgrel=2
pkgdesc='A library for handling OpenGL function pointer management for you'
arch=(any)
url='https://github.com/anholt/libepoxy'
license=(MIT)
groups=(afm-$CARCH)
depends=(mingw-w64-crt)
makedepends=(
    afm-mingw-w64-makedepends
    python
)
source=("https://github.com/anholt/libepoxy/releases/download/$pkgver/libepoxy-$pkgver.tar.xz")
md5sums=('a0dc66910009a99c47c5def50b042d77')

prepare() {
  cd "$srcdir/libepoxy-$pkgver"

  autoreconf -ivf
}

build() {
  local tardir="$srcdir/libepoxy-$pkgver"
  local builddir="$tardir-build"

  mkdir -p "$builddir"
  cd "$builddir"
  source $CHOST-configure "$tardir" \
    --enable-static \
    --enable-shared \
    --disable-silent-rules
  make
}

package() {
  local prefix="$pkgdir/usr/$CHOST"

  cd "$srcdir/libepoxy-$pkgver-build"
  make DESTDIR="$pkgdir" install

  find "$prefix" -name '*.exe' -exec $CHOST-strip {} +
  find "$prefix" -name '*.dll' -exec $CHOST-strip --strip-unneeded {} +
  find "$prefix" -name '*.a'   -exec $CHOST-strip -g {} +
}

# vim:set ts=2 sw=2 et:
