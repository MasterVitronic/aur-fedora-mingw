# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# This package automatically pulls in the makedepends needed for MinGW
# cross-building and provides the following helper stuff:
#
# - pkg-config wrappers
# - CMake configurations for MinGW
# - configure wrappers
#
# It borrows ideas from the mingw-filesystem Fedora package [1] and some
# AUR packages [2] [3] that in my opinion has wrong names.
#
# [1] http://pkgs.fedoraproject.org/cgit/mingw-filesystem.git/
# [2] https://aur.archlinux.org/packages/mingw-w64-pkg-config/
# [3] https://aur.archlinux.org/packages/mingw-w64-cmake/

pkgname=afm-mingw-w64-makedepends
pkgver=2
pkgrel=4
pkgdesc='A collection of stuff needed for MinGW cross-building'
arch=(any)
url='http://dev.entidi.com/p/aur-fedora-mingw/'
license=(mit)
groups=(aur-fedora-mingw)
depends=(mingw-w64-gcc)
conflicts=(mingw-w64-pkg-config mingw-w64-cmake)
provides=(mingw-w64-pkg-config mingw-w64-cmake)
source=("i686-w64-mingw32-cmake"
        "i686-w64-mingw32-configure"
        "i686-w64-mingw32-pkg-config"
        "mingw-configure"
        "x86_64-w64-mingw32-cmake"
        "x86_64-w64-mingw32-configure"
        "x86_64-w64-mingw32-pkg-config"
        "Toolchain-i686-w64-mingw32.cmake"
        "Toolchain-x86_64-w64-mingw32.cmake")
md5sums=('76d24f33081e428a11e8cb20e6067c25'
         '677ddd6208dfaa02db5d9a1449edf05e'
         '2c2641547debc5d289bb85b0939e4380'
         'ff9c5611dccaedb7b87f92f77ea082f0'
         '5fcedae7d788c4fd9100d139bf80f5aa'
         '4ce073a3b299d6491dc180def6e9fc25'
         '0a879bf20cd82e44b73eb49cf2484eda'
         '4704995815374af7b3b853b139d04ad5'
         'cdb9f76c5ab95038df1c03755e9119e7')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

package() {
  local arch

  install -d "$pkgdir/usr/bin"
  install -d "$pkgdir/usr/share/mingw"

  # This should be bound to the configure wrappers
  install -m 644 "mingw-configure" "$pkgdir/usr/bin"

  for arch in ${_architectures}; do
    # configure wrappers
    install -m 755 $arch-configure "$pkgdir/usr/bin"

    # pkg-config
    install -m 755 $arch-pkg-config "$pkgdir/usr/bin"

    # CMake
    install -m 644 Toolchain-$arch.cmake "$pkgdir/usr/share/mingw/"
    install -m 755 $arch-cmake "$pkgdir/usr/bin/"
  done
}
