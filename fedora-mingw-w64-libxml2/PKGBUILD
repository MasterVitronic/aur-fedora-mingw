# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-libxml2.git/

_realname=libxml2
pkgname="fedora-mingw-w64-${_realname}"
pkgver=2.9.3
pkgrel=1
pkgdesc='XML parsing library, version 2 (mingw-w64)'
arch=(any)
url='http://www.xmlsoft.org/'
license=(MIT)
groups=(fedora-mingw)
depends=(mingw-w64-crt mingw-w64-zlib)
makedepends=(mingw-w64-gcc mingw-w64-gettext)
provides=(mingw-w64-libxml2=$pkgver)
conflicts=(mingw-w64-libxml2)
options=(!strip staticlibs !buildflags)
source=("ftp://ftp.xmlsoft.org/libxml2/libxml2-$pkgver.tar.gz"
        "http://pkgs.fedoraproject.org/cgit/mingw-libxml2.git/plain/mingw32-libxml2-static-build-compile-fix.patch?id=b2ef773d9a92f507605e3fa6bb52c91a0119d16c")
md5sums=('daece17e045f1c107610e137ab50c179'
         '0df377025082cd93cccbca547f048011')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

prepare() {
  cd "$srcdir/${_realname}-$pkgver"

  # Avoid corrupting the xml catalogs
  # https://bugzilla.redhat.com/show_bug.cgi?id=1153753
  patch -Np1 < '../libxml2-2.9.2-catalog-revert.patch?id=47ad62add5888cb5be7b2781b0197d4201b151a0'

  # MinGW-specific patches
  patch -Np0 < '../mingw32-libxml2-static-build-compile-fix.patch?id=b2ef773d9a92f507605e3fa6bb52c91a0119d16c'
}

build() {
  local tardir="$srcdir/${_realname}-$pkgver"
  local builddir
  local arch

  # XXX: disabling zlib for now (--without-zlib) because the
  # compilation fails otherwise with undefined references.
  # Not sure if this is an issue of zlib of libxml2 though.
  for arch in ${_architectures[@]}; do
    builddir="$tardir-build-$arch"

    # LibXML2 can't build static and shared libraries in one go, so we
    # build LibXML2 twice here
    mkdir -p "$builddir-static"
    cd "$builddir-static"
    source $arch-configure "$tardir" \
      --enable-static \
      --disable-shared \
      CPPFLAGS='-DLIBXML_STATIC_FOR_DLL' \
      --without-python \
      --with-modules \
      --with-threads=win32 \
      --without-zlib
    make

    mkdir -p "$builddir-shared"
    cd "$builddir-shared"
    source $arch-configure "$tardir" \
      --disable-static \
      --enable-shared \
      --without-python \
      --with-modules \
      --with-threads=win32 \
      --without-zlib
    make
  done
}

package() {
  local arch
  local prefix
  local builddir

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"
    builddir="$srcdir/${_realname}-$pkgver-build-$arch"

    # First install all the files belonging to the shared build
    cd "$builddir-shared"
    make DESTDIR="$pkgdir" install

    # Then manually install the static library
    install -t "$prefix/lib" "$builddir-static/.libs/libxml2.a"

    find "$prefix" -name '*.dll' -exec $arch-strip --strip-unneeded {} +
    find "$prefix" -name '*.a'   -exec $arch-strip -g {} +

    rm -fr "$prefix/share"
  done
}

# vim:set ts=2 sw=2 et:
