# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-expat.git/

pkgname=fedora-mingw-w64-expat
pkgver=2.1.0
pkgrel=7
pkgdesc='An XML parser library (mingw-w64)'
arch=(any)
url='http://expat.sourceforge.net/'
license=(custom)
groups=(fedora-mingw)
depends=(mingw-w64-crt)
makedepends=(mingw-w64-gcc)
provides=("mingw-w64-expat=$pkgver")
conflicts=(mingw-w64-expat)
options=(!strip !buildflags !libtool)
source=("http://downloads.sourceforge.net/sourceforge/expat/expat-$pkgver.tar.gz")
md5sums=('dd7dab7a5fea97d2a6a43f511449b7cd')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

build() {
  local tardir="$srcdir/${pkgname#fedora-mingw-w64-}-$pkgver"
  local builddir
  local arch

  export CFLAGS='-O2 -pipe'
  export CXXFLAGS="$CFLAGS"
  unset  LDFLAGS
  unset  CPPFLAGS

  for arch in ${_architectures[@]}; do
    builddir="$srcdir/$pkgname-$pkgver-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    "$tardir/configure" \
      --prefix="/usr/$arch" \
      --host="$arch"
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/$pkgname-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    find "$prefix" -regex '.*\.\(exe\|bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip -xg {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +
    rm -fr "$prefix/share"
  done
}

# vim:set ts=2 sw=2 et:
