# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-expat.git/

pkgname=$CARCH-mingw-w64-expat
pkgver=2.2.0
pkgrel=3
pkgdesc='An XML parser library (mingw-w64)'
arch=(any)
url='http://expat.sourceforge.net/'
license=(custom)
groups=(afm-$CARCH)
depends=(mingw-w64-crt)
makedepends=(afm-mingw-w64-makedepends)
source=("http://downloads.sourceforge.net/sourceforge/expat/expat-$pkgver.tar.bz2")
md5sums=('2f47841c829facb346eb6e3fab5212e2')

build() {
  local tardir="$srcdir/expat-$pkgver"
  local builddir="$tardir-build"

  mkdir -p "$builddir"
  cd "$builddir"
  source $CHOST-configure "$tardir"
  make
}

package() {
  local prefix="$pkgdir/usr/$CHOST"

  cd "$srcdir/expat-$pkgver-build"
  make DESTDIR="$pkgdir" install

  find "$prefix" -name '*.exe' -exec $CHOST-strip {} +
  find "$prefix" -name '*.dll' -exec $CHOST-strip --strip-unneeded {} +
  find "$prefix" -name '*.a'   -exec $CHOST-strip -g {} +
}

# vim:set ts=2 sw=2 et:
