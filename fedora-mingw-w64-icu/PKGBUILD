# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-icu.git/

pkgname=$CARCH-mingw-w64-icu
pkgver=57.1
pkgrel=4
pkgdesc='International Components for Unicode library (mingw-w64)'
arch=(any)
url='http://www.icu-project.org/'
license=(custom:icu)
groups=(afm-$CARCH)
depends=(
    mingw-w64-crt
    $CARCH-mingw-w64-fontconfig
    $CARCH-mingw-w64-freetype
    $CARCH-mingw-w64-libxml2
    $CARCH-mingw-w64-pixman
    $CARCH-mingw-w64-libpng
    $CARCH-mingw-w64-win-iconv
    $CARCH-mingw-w64-zlib
    $CARCH-mingw-w64-glib2
)
makedepends=(afm-mingw-w64-makedepends)
source=("http://download.icu-project.org/files/icu4c/$pkgver/icu4c-${pkgver//./_}-src.tgz"
        "http://pkgs.fedoraproject.org/cgit/rpms/mingw-icu.git/plain/icu4c-56_1-crossbuild.patch?id=1fcee96b58739f449cbc6fb5eea09391f3160e38")
md5sums=('976734806026a4ef8bdd17937c8898b9'
         'b56614627ea08022d9044053c2e70aa2')

prepare() {
  cd "$srcdir/icu"

  # Patch to fix the build from
  # https://build.opensuse.org/package/show/windows:mingw:win32/mingw32-icu
  patch -Np1 < '../icu4c-56_1-crossbuild.patch?id=1fcee96b58739f449cbc6fb5eea09391f3160e38'
}

build() {
  local tardir="$srcdir/icu/source"
  local builddir="$srcdir/icu-$pkgver-build"

  # First pass: native build
  mkdir -p "$builddir-native"
  cd "$builddir-native"
  (
    # Do it in a subshell so the environment can be set accordingly
    # and it will not be leaked outside
    source /etc/makepkg.conf
    "$tardir/configure" \
      --disable-shared \
      --enable-static
    make
  )

  # Second pass: cross compilation.
  mkdir -p "$builddir"
  cd "$builddir"
  (
    source $CHOST-configure "$tardir" \
      --disable-static \
      --enable-shared \
      --with-cross-build="$builddir-native" \
      --with-data-packaging=library
    make
  )
}

package() {
  local prefix="$pkgdir/usr/$CHOST"
  local api=${pkgver%%.*}

  cd "$srcdir/icu-$pkgver-build"
  make DESTDIR="$pkgdir" install

  mv "$prefix"/lib/*.dll "$prefix/bin/"

  find "$prefix" -name '*.exe' -exec $CHOST-strip {} +
  find "$prefix" -name '*.dll' -exec $CHOST-strip --strip-unneeded {} +
  find "$prefix" -name '*.a'   -exec $CHOST-strip -g {} +

  rm -f "$prefix/bin/icu-config"
  rm -fr "$prefix/lib/icu"
}

# vim:set ts=2 sw=2 et:
