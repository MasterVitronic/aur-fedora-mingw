# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-icu.git/

_realname=icu
pkgname="fedora-mingw-w64-${_realname}"
pkgver=50.1.2
pkgrel=10
pkgdesc='International Components for Unicode library (mingw-w64)'
arch=(any)
url='http://www.icu-project.org/'
license=(custom:icu)
groups=(fedora-mingw)
depends=(mingw-w64-crt mingw-w64-fontconfig mingw-w64-freetype
         mingw-w64-libxml2 mingw-w64-pixman mingw-w64-libpng
         mingw-w64-win-iconv mingw-w64-zlib mingw-w64-glib2)
makedepends=(afm-mingw-w64-makedepends)
provides=(mingw-w64-icu=$pkgver)
conflicts=(mingw-w64-icu)
options=(!strip staticlibs !buildflags)
source=("http://download.icu-project.org/files/icu4c/$pkgver/icu4c-${pkgver//./_}-src.tgz"
        "http://pkgs.fedoraproject.org/cgit/mingw-icu.git/plain/icu4c-4_6_1-crossbuild.patch?id=f7a0fef35cf370c8c0add164e72499f05817cc79"
        "http://pkgs.fedoraproject.org/cgit/mingw-icu.git/plain/icu4c-4_6_1-use-correct-genccode-assembly-on-win32-and-win64.patch?h=f18"
        "http://pkgs.fedoraproject.org/cgit/mingw-icu.git/plain/icu4c-4_6_1-mingw-w64-mkdir-compatibility.patch?id=b942c392347ba1b68c4aa0603a774cecf5035915"
        "http://pkgs.fedoraproject.org/cgit/mingw-icu.git/plain/icu.10318.CVE-2013-2924_changeset_34076.patch?id=ecbc40f40b154a47ea82410d86ef580907338ef1")
md5sums=('beb98aa972219c9fcd9c8a71314943c9'
         '91358645bc0a6b4d2b502f43cb7293f4'
         'c480877485438a1f677ca23f7d915692'
         'c1194c6ac4bc02bd46f127a457ab4e93'
         'c145037f754572ef88d1a693e86991da')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

prepare() {
  cd "$srcdir/${_realname}"

  patch -Np1 < '../icu4c-4_6_1-crossbuild.patch?id=f7a0fef35cf370c8c0add164e72499f05817cc79'
  patch -Np1 < '../icu4c-4_6_1-use-correct-genccode-assembly-on-win32-and-win64.patch?h=f18'
  patch -Np1 < '../icu4c-4_6_1-mingw-w64-mkdir-compatibility.patch?id=b942c392347ba1b68c4aa0603a774cecf5035915'
  patch -Np1 < '../icu.10318.CVE-2013-2924_changeset_34076.patch?id=ecbc40f40b154a47ea82410d86ef580907338ef1'

  # Needed by genccode patch
  cd source
  autoconf -f
}

build() {
  local tardir="$srcdir/${_realname}/source"
  local builddir
  local arch

  unset LDFLAGS CPPFLAGS

  for arch in ${_architectures[@]}; do
    builddir="$srcdir/${_realname}-$pkgver-build-$arch"

    # First pass: native build
    mkdir -p "$builddir-native"
    cd "$builddir-native"
    "$tardir/configure" \
      --disable-shared \
      --enable-static
    make

    # Second pass: cross compilation. Enclose it in a subshell so
    # there will be no environment variable leaks from $arch-configure.
    mkdir -p "$builddir"
    cd "$builddir"
    (
      source "$arch-configure" "$tardir" \
        --disable-static \
        --enable-shared \
        --with-cross-build="$builddir-native" \
        --with-data-packaging=library
    )
    make
  done
}

package() {
  local arch
  local prefix
  local api

  api=${pkgver%%.*}

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/${_realname}-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    find "$pkgdir" -name '*.dll' -type l -delete
    mv -f "$prefix/lib/libicudata$api.dll" "$prefix/lib/icudata$api.dll"
    mv "$prefix"/lib/*.dll "$prefix/bin/"
    find "$prefix" -regex '.*\.\(exe\|bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip -xg {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +

    rm -f "$prefix/bin/icu-config"
    rm -fr "$prefix/share"
    rm -fr "$prefix/lib/icu"
  done
}

# vim:set ts=2 sw=2 et:
