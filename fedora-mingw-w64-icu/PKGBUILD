# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-icu.git/

_realname=icu
pkgname="fedora-mingw-w64-${_realname}"
pkgver=57.1
pkgrel=1
pkgdesc='International Components for Unicode library (mingw-w64)'
arch=(any)
url='http://www.icu-project.org/'
license=(custom:icu)
groups=(aur-fedora-mingw fedora-mingw)
depends=(mingw-w64-crt mingw-w64-fontconfig mingw-w64-freetype
         mingw-w64-libxml2 mingw-w64-pixman mingw-w64-libpng
         mingw-w64-win-iconv mingw-w64-zlib mingw-w64-glib2)
makedepends=(afm-mingw-w64-makedepends)
provides=(mingw-w64-icu=$pkgver)
conflicts=(mingw-w64-icu)
options=(!strip staticlibs !buildflags)
source=("http://download.icu-project.org/files/icu4c/$pkgver/icu4c-${pkgver//./_}-src.tgz"
        "http://pkgs.fedoraproject.org/cgit/rpms/mingw-icu.git/plain/icu4c-56_1-crossbuild.patch?id=1fcee96b58739f449cbc6fb5eea09391f3160e38")
md5sums=('976734806026a4ef8bdd17937c8898b9'
         'b56614627ea08022d9044053c2e70aa2')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

prepare() {
  cd "$srcdir/${_realname}"

  # Patch to fix the build from
  # https://build.opensuse.org/package/show/windows:mingw:win32/mingw32-icu
  patch -Np1 < '../icu4c-56_1-crossbuild.patch?id=1fcee96b58739f449cbc6fb5eea09391f3160e38'
}

build() {
  local tardir="$srcdir/${_realname}/source"
  local builddir
  local arch

  for arch in ${_architectures[@]}; do
    builddir="$srcdir/${_realname}-$pkgver-build-$arch"

    # First pass: native build
    mkdir -p "$builddir-native"
    cd "$builddir-native"
    "$tardir/configure" \
      --disable-shared \
      --enable-static
    make

    # Second pass: cross compilation. Enclose it in a subshell so
    # there will be no environment variable leaks from $arch-configure.
    mkdir -p "$builddir"
    cd "$builddir"
    (
      source $arch-configure "$tardir" \
        --disable-static \
        --enable-shared \
        --with-cross-build="$builddir-native" \
        --with-data-packaging=library
      make
    )
  done
}

package() {
  local arch
  local prefix
  local api

  api=${pkgver%%.*}

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/${_realname}-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    mv   "$prefix"/lib/*.dll "$prefix/bin/"
    find "$prefix" -regex '.*\.\(exe\|bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip --strip-unneeded {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +

    rm -f "$prefix/bin/icu-config"
    rm -fr "$prefix/share"
    rm -fr "$prefix/lib/icu"
  done
}

# vim:set ts=2 sw=2 et:
