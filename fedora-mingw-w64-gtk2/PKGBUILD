# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-gtk2.git/

_realname=gtk+
pkgname=fedora-mingw-w64-gtk2
pkgver=2.24.30
pkgrel=1
pkgdesc='GTK+2 is a multi-platform toolkit (mingw-w64)'
arch=(any)
url='http://www.gtk.org/'
license=(LGPL)
groups=(aur-fedora-mingw fedora-mingw)
depends=(mingw-w64-crt fedora-mingw-w64-atk fedora-mingw-w64-cairo
         fedora-mingw-w64-gdk-pixbuf fedora-mingw-w64-gettext
         fedora-mingw-w64-glib2 fedora-mingw-w64-win-iconv
         fedora-mingw-w64-pango fedora-mingw-w64-pixman mingw-w64-zlib)
makedepends=(afm-mingw-w64-makedepends mingw-w64-gobject-introspection wine)
provides=(mingw-w64-gtk2=$pkgver)
conflicts=(mingw-w64-gtk2)
options=(!strip staticlibs !buildflags)
source=("http://ftp.gnome.org/pub/GNOME/sources/${_realname}/${pkgver%.*}/${_realname}-$pkgver.tar.xz"
        "http://pkgs.fedoraproject.org/cgit/mingw-gtk2.git/plain/mingw32-gtk2-2.15.0-xbuttons.patch?h=f18"
        "http://pkgs.fedoraproject.org/cgit/mingw-gtk2.git/plain/mingw32-gtk2-enable_static_build.patch?h=f18"
        "http://pkgs.fedoraproject.org/cgit/rpms/mingw-gtk2.git/plain/gtk-fix-out-of-source-build.patch?id=a1c15a29565441dcbf119168eb44b1e20a7069c2"
        "http://pkgs.fedoraproject.org/cgit/mingw-gtk2.git/plain/icon-padding.patch?id=95be474adc76e60caa692330cf2706b90848146f"
        "http://pkgs.fedoraproject.org/cgit/mingw-gtk2.git/plain/tooltip-positioning.patch?id=95be474adc76e60caa692330cf2706b90848146f"
        "http://pkgs.fedoraproject.org/cgit/mingw-gtk2.git/plain/window-dragging.patch?id=95be474adc76e60caa692330cf2706b90848146f"
        "http://pkgs.fedoraproject.org/cgit/mingw-gtk2.git/plain/gtk-combo-box-dont-use-invalidated-iter.patch?id=1296c449f78d4ef378541311bb4a4833c6a26fe6"
        "0011-gir-for-gdkwin32.mingw.patch"
        "gtk.immodules")
md5sums=('04568ba5c58b75e3c7543e45628ad789'
         '44ca5d1ce91f5364182c6ea2614c7688'
         '396835720e308acaa0372e5a45c463d4'
         '64a1c29cbda543fdd6d5178417a478c7'
         '5c6d42eb6651106f605c2ac9d92a3951'
         '86159ea0934b25a7bbda9aa436ffc551'
         '147a90540b678b941e7d5d71054b2447'
         '1cef826891d79b06ae69cf632b358d73'
         '91d12fc6da9026deb0f94f38ab22a3ec'
         '9967ded27c0eb3b615acae7539621607')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

prepare() {
  cd "$srcdir/${_realname}-$pkgver"

  # Fix use of extended buttons in gtkstatusicon.
  patch -Np1 < '../mingw32-gtk2-2.15.0-xbuttons.patch?h=f18'

  # https://bugzilla.gnome.org/show_bug.cgi?id=583273
  patch -Np1 < '../icon-padding.patch?id=95be474adc76e60caa692330cf2706b90848146f'

  # https://bugzilla.gnome.org/show_bug.cgi?id=599618
  patch -Np1 < '../tooltip-positioning.patch?id=95be474adc76e60caa692330cf2706b90848146f'

  # https://bugzilla.gnome.org/show_bug.cgi?id=611313
  patch -Np1 < '../window-dragging.patch?id=95be474adc76e60caa692330cf2706b90848146f'

  # Don't use invalidated iter in GtkComboBox
  # Resolves segfaults in GtkFileChooserButton
  # https://bugzilla.redhat.com/show_bug.cgi?id=985559
  # https://bugzilla.gnome.org/show_bug.cgi?id=704508
  patch -Np1 < '../gtk-combo-box-dont-use-invalidated-iter.patch?id=1296c449f78d4ef378541311bb4a4833c6a26fe6'

  # Enable introspection, patch from LRN:
  patch -Np1 < '../0011-gir-for-gdkwin32.mingw.patch'

  autoreconf -ivf

  # Enable building a static library of GTK
  patch -Np0 < '../mingw32-gtk2-enable_static_build.patch?h=f18'

  # GTK doesn't support out-of-source builds
  patch -Np0 < '../gtk-fix-out-of-source-build.patch?id=a1c15a29565441dcbf119168eb44b1e20a7069c2'

  # The pre-generated gtk.def file can't be used for MinGW-W64
  # Force a regeneration of this file by removing the bundled copy
  rm -f gtk/gtk.def
}

build() {
  local tardir="$srcdir/${_realname}-$pkgver"
  local builddir
  local arch

  for arch in ${_architectures[@]}; do
    builddir="$tardir-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    source $arch-configure "$tardir" \
      --enable-static \
      --enable-shared \
      --disable-silent-rules \
      --enable-introspection \
      --disable-cups \
      --disable-gtk-doc
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/${_realname}-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    # Remove all the exe files that do not have gtk2 or 2.0 inside their
    # names, hence possibly conflicting with GTK+3
    find "$prefix" -name '*.exe' -a ! -name '*gtk2*' -a ! -name '*2.0*' -exec rm -f {} +

    find "$prefix" -regex '.*\.\(bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip --strip-unneeded {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +

    rm -fr "$prefix/share/aclocal"
    rm -fr "$prefix/share/man"
  done
}

# vim:set ts=2 sw=2 et:
