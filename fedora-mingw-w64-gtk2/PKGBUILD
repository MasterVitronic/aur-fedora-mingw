# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-gtk2.git/

pkgname=fedora-mingw-w64-gtk2
pkgver=2.24.17
pkgrel=3
pkgdesc='GTK+2 is a multi-platform toolkit (mingw-w64)'
arch=(any)
url='http://www.gtk.org/'
license=(LGPL)
groups=(fedora-mingw)
depends=(mingw-w64-crt fedora-mingw-w64-atk fedora-mingw-w64-cairo
         fedora-mingw-w64-gettext fedora-mingw-w64-glib2
         fedora-mingw-w64-win-iconv fedora-mingw-w64-pango
         fedora-mingw-w64-gdk-pixbuf fedora-mingw-w64-pixman mingw-w64-zlib)
makedepends=(mingw-w64-gcc mingw-w64-pkg-config
             autoconf automake libtool gtk-doc gobject-introspection)
provides=("mingw-w64-gtk2=$pkgver")
conflicts=(mingw-w64-gtk2)
options=(!strip !buildflags !libtool)
source=("http://ftp.gnome.org/pub/GNOME/sources/gtk+/${pkgver%.*}/gtk+-$pkgver.tar.xz"
        "http://pkgs.fedoraproject.org/cgit/mingw-gtk2.git/plain/mingw32-gtk2-2.15.0-xbuttons.patch?h=f18"
        "http://pkgs.fedoraproject.org/cgit/mingw-gtk2.git/plain/mingw32-gtk2-enable_static_build.patch?h=f18"
        "http://pkgs.fedoraproject.org/cgit/mingw-gtk2.git/plain/gtk-fix-out-of-source-build.patch?h=f18"
        "http://pkgs.fedoraproject.org/cgit/mingw-gtk2.git/plain/icon-padding.patch?id=95be474adc76e60caa692330cf2706b90848146f"
        "http://pkgs.fedoraproject.org/cgit/mingw-gtk2.git/plain/tooltip-positioning.patch?id=95be474adc76e60caa692330cf2706b90848146f"
        "http://pkgs.fedoraproject.org/cgit/mingw-gtk2.git/plain/window-dragging.patch?id=95be474adc76e60caa692330cf2706b90848146f"
        "http://pkgs.fedoraproject.org/cgit/mingw-gtk2.git/plain/gtk-dont-define-initguid.patch?id=3224e445dad240da5f5ebfe371e8c9f627459089"
        "gtk.immodules")
md5sums=('a10cc43fad8d64f8893d779b1f8322ff'
         '44ca5d1ce91f5364182c6ea2614c7688'
         '396835720e308acaa0372e5a45c463d4'
         '8231dd2b7484574968d5faa57262af2a'
         '5c6d42eb6651106f605c2ac9d92a3951'
         '86159ea0934b25a7bbda9aa436ffc551'
         '147a90540b678b941e7d5d71054b2447'
         '4038939df90f80ea6923d67afff28e03'
         '9967ded27c0eb3b615acae7539621607')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

build() {
  # gtk2 != gtk+, so hardcoding it in $tardir
  local tardir="$srcdir/gtk+-$pkgver"
  local builddir
  local arch

  # Apply patches from fedora repository
  cd "$tardir"
  patch -Np1 < '../mingw32-gtk2-2.15.0-xbuttons.patch?h=f18'
  # https://bugzilla.gnome.org/show_bug.cgi?id=583273
  patch -Np1 < '../icon-padding.patch?id=95be474adc76e60caa692330cf2706b90848146f'
  # https://bugzilla.gnome.org/show_bug.cgi?id=599618
  patch -Np1 < '../tooltip-positioning.patch?id=95be474adc76e60caa692330cf2706b90848146f'
  # https://bugzilla.gnome.org/show_bug.cgi?id=611313
  patch -Np1 < '../window-dragging.patch?id=95be474adc76e60caa692330cf2706b90848146f'
  # As of mingw-w64 r5589 the header knownfolders.h was added
  # which contains definitions like FOLDERID_Windows
  # As these are also part of libuuid.a you'll get linker failures like:
  # /usr/i686-w64-mingw32/sys-root/mingw/lib/libuuid.a(lib32_libuuid_a-uuid.o):(.rdata+0x0): multiple definition of `FOLDERID_Windows'
  # win32/.libs/libgdk-win32.a(gdkdnd-win32.o):gdkdnd-win32.c:(.rdata+0x360): first defined here
  #
  # Workaround this for now until mingw-w64 upstream has come up
  # with a statement whether this is expected behaviour and what
  # component should be fixed exactly
  #
  # http://mingw-w64.svn.sourceforge.net/viewvc/mingw-w64?view=revision&revision=5589
  patch -Np0 < '../gtk-dont-define-initguid.patch?id=3224e445dad240da5f5ebfe371e8c9f627459089'
  autoreconf -if
  patch -Np0 < '../mingw32-gtk2-enable_static_build.patch?h=f18'
  patch -Np0 < '../gtk-fix-out-of-source-build.patch?h=f18'

  # The pre-generated gtk.def file can't be used for MinGW-W64
  # Force a regeneration of this file by removing the bundled copy
  rm -f gtk/gtk.def

  export CFLAGS='-O2 -pipe'
  export CXXFLAGS="$CFLAGS"
  unset  LDFLAGS
  unset  CPPFLAGS

  for arch in ${_architectures[@]}; do
    builddir="$srcdir/$pkgname-$pkgver-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    "$tardir/configure" \
      --prefix="/usr/$arch" \
      --host="$arch" \
      --disable-cups \
      --enable-static \
      --disable-gtk-doc
    make
  done
}

package() {
  local arch
  local prefix

  for arch in ${_architectures[@]}; do
    prefix="$pkgdir/usr/$arch"

    cd "$srcdir/$pkgname-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install

    # Do not remove exe files (leave gtk2-demo.exe for testing purpose)
    find "$prefix" -regex '.*\.\(bat\|def\|exp\)' -exec rm -f {} +
    find "$prefix" -name '*.dll' -exec $arch-strip -xg {} +
    find "$prefix" -name '*.a' -exec $arch-strip -g {} +
    #rm -fr "$prefix/share"
  done
}

# vim:set ts=2 sw=2 et:
