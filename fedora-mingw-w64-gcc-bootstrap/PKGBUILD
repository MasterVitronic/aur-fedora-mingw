# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-gcc.git/
# compiled for bootstrapping. It is not intended for general use:
# see ../README.md for details on the bootstrap process.

pkgname=fedora-mingw-w64-gcc-bootstrap
pkgver=4.8.2
pkgrel=3
pkgdesc="Cross GCC for the MinGW-w64 cross-compiler (bootstrap)"
arch=('i686' 'x86_64')
url="http://gcc.gnu.org"
license=('GPL' 'LGPL' 'FDL' 'custom')
groups=('fedora-mingw-bootstrap')
depends=('zlib' 'libmpc' 'cloog'
         'mingw-w64-binutils')
makedepends=('mingw-w64-headers' 'mingw-w64-gcc')
optdepends=()
provides=("mingw-w64-gcc=$pkgver")
conflicts=(mingw-w64-gcc)
replaces=()
backup=()
options=(!strip !buildflags !libtool !emptydirs)
source=("ftp://gcc.gnu.org/pub/gcc/releases/gcc-$pkgver/gcc-$pkgver.tar.bz2")
md5sums=('a3d7d63b9cb6b6ea049469a0c4a43c9d')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

build() {
  local tardir="$srcdir/gcc-$pkgver"
  local builddir
  local arch

  cd "$tardir"
  echo 'Fedora-archlinux MinGW $pkgver-$pkgrel' > gcc/DEV-PHASE

  # using -pipe causes spurious test-suite failures
  # http://gcc.gnu.org/bugzilla/show_bug.cgi?id=48565
  export CFLAGS='-O2'
  export CXXFLAGS="$CFLAGS"
  unset  LDFLAGS
  unset  CPPFLAGS

  for arch in ${_architectures[@]}; do
    builddir="$srcdir/$pkgname-$pkgver-build-$arch"
    mkdir -p "$builddir"
    cd "$builddir"
    $tardir/configure \
        --prefix=/usr \
        --with-gxx-include-dir=/usr/$arch/include/c++ \
        --target=$arch \
        --disable-lto \
        --disable-libgomp \
        --with-gnu-as \
        --with-gnu-ld \
        --without-newlib \
        --disable-multilib \
        --disable-plugin \
        --with-system-zlib \
        --disable-nls \
        --without-included-gettext \
        --disable-win32-registry \
        --enable-languages="c,c++" \
        --with-bugurl=http://bugzilla.redhat.com/bugzilla
    make all-gcc
  done
}

package() {
  local arch

  for arch in ${_architectures[@]}; do
    cd "$srcdir/$pkgname-$pkgver-build-$arch"
    make DESTDIR="$pkgdir" install-gcc
  done

  find "$pkgdir" -name '*.dll' -exec $arch-strip -xg {} +
  find "$pkgdir" -name '*.a' -exec $arch-strip -g {} +
  strip "$pkgdir"/usr/bin/*
  rm -fr "$pkgdir/usr/share"
}

# vim:set ts=2 sw=2 et:
