# Maintainer: Nicola Fontana <ntd@entidi.it>
#
# Port of the Fedora package found at
# http://pkgs.fedoraproject.org/cgit/mingw-termcap.git/
#
# Note: Termcap was deprecated and removed from Fedora after F-8.  It
# has been replaced by ncurses.  However ncurses cannot be compiled on
# Windows so we have to supply termcap.  In addition, the last stand-
# alone Fedora termcap package was actually just /etc/termcap from
# ncurses.  So here we are using the GNU termcap library which is
# regretably GPL'd.

pkgname="$CARCH-mingw-w64-termcap"
pkgver=1.3.1
pkgrel=17
pkgdesc='MinGW terminal feature database (mingw-w64)'
arch=(any)
url='ftp://ftp.gnu.org/gnu/termcap/'
license=(GPL)
groups=(afm-$CARCH)
depends=(mingw-w64-crt)
makedepends=(afm-mingw-w64-makedepends)
source=("ftp://ftp.gnu.org/gnu/termcap/termcap-$pkgver.tar.gz")
md5sums=('ffe6f86e63a3a29fa53ac645faaabdfa')

build() {
  local tardir="$srcdir/termcap-$pkgver"
  local builddir="$tardir-build"

  mkdir -p "$builddir"
  cd "$builddir"

  # This old configure script needs some help to understand what is needed.
  ac_cv_c_cross=1 "$tardir/configure" \
    --build="$BUILDHOST" \
    --prefix="/usr/$CHOST" \
    --host="$CHOST"
  make \
    CC="$CHOST-gcc" \
    AR="$CHOST-ar" \
    RANLIB="$CHOST-ranlib"

  # Manually build the shared library. No need for -fPIC on Windows.
  $CHOST-gcc -shared $LDFLAGS \
    -Wl,--out-implib,libtermcap.dll.a \
    -o libtermcap-0.dll \
    termcap.o tparam.o version.o
}

package() {
  local builddir="$srcdir/termcap-$pkgver-build"
  local prefix="$pkgdir/usr/$CHOST"

  # make DESTDIR=... is not supported.
  make install -C "$builddir" \
    prefix="$prefix" \
    exec_prefix="$prefix" \
    infodir="$prefix/share/info" \
    oldincludedir=

  mkdir -p "$prefix/bin"
  mkdir -p "$prefix/lib"
  install -m 0755 "$builddir/libtermcap-0.dll" "$prefix/bin"
  install -m 0755 "$builddir/libtermcap.dll.a" "$prefix/lib"

  find "$prefix" -name '*.exe' -exec $CHOST-strip {} +
  find "$prefix" -name '*.dll' -exec $CHOST-strip --strip-unneeded {} +
  find "$prefix" -name '*.a'   -exec $CHOST-strip -g {} +
}

# vim:set ts=2 sw=2 et:
