# Maintainer: Nicola Fontana <ntd@entidi.it>
# Maintainer: Alexey Pavlov <alexpux@gmail.com>
#
# This is an adaptation of the MinGW package by Alexpux found on GitHub:
# https://github.com/Alexpux/MINGW-packages/tree/master/mingw-w64-lua

_realname=lua
pkgname="mingw-w64-${_realname}"
pkgver=5.2.4
pkgrel=1
pkgdesc='Powerful light-weight programming language designed for extending applications (mingw-w64)'
arch=(any)
url='http://www.lua.org/'
license=(MIT)
depends=(mingw-w64-crt)
makedepends=(mingw-w64-gcc)
options=(!strip staticlibs !buildflags)
source=("http://www.lua.org/ftp/${_realname}-$pkgver.tar.gz"
        "lua.pc")
md5sums=('913fdb32207046b273fdb17aad70be13'
         'e7ba6c2b695b0b84a5ea0cbff5fc9067')

_architectures=(i686-w64-mingw32 x86_64-w64-mingw32)

package() {
  local tardir="$srcdir/${_realname}-$pkgver"
  local api="${pkgver%.*}"
  local arch
  local builddir
  local prefix

  unset LDFLAGS CPPFLAGS

  for arch in ${_architectures[@]}; do
    builddir="$tardir-build-$arch"
    prefix="$pkgdir/usr/$arch"

    # Lua does not support VPATH builds
    cp -fr "$tardir" "$builddir"
    sed -e "s|%VER%|$api|g" \
        -e "s|%REL%|$pkgver|g" \
        -e "s|/usr|/usr/$arch|g" \
        "$srcdir/lua.pc" > "$builddir/lua.pc"

    # Building and installing
    cd "$builddir"
    make -j1 \
      AR="$arch-ar rcu" \
      RANLIB="$arch-ranlib" \
      STRIP="$arch-strip" \
      CC="$arch-gcc" \
      INSTALL_TOP="$prefix" \
      TO_BIN="lua.exe luac.exe lua52.dll" \
      mingw install

    # Manually install the pkg-config file
    install -Dm644 "$builddir/lua.pc" "$prefix/lib/pkgconfig/lua$api.pc"
    # This symlink should be done only for the current Lua release
    ln -s "lua$api.pc" "$prefix/lib/pkgconfig/lua.pc"
  done
}

# vim:set ts=2 sw=2 et:
